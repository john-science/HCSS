##############################################################################
# temple.des: Entries for the Ecumenical Temple, and Temples themselves
##############################################################################

{{
-- Call this function, with the default being the top parameter,
-- for any temple with variable altar totals.
function get_altar_count(e, default)
  local params = dgn.map_parameters()
  local count = "";
  if params ~= nil then
    local tag = unpack(params)
    if tag ~= nil then
      -- temple_altars_ is 14 characters
      count = string.sub(tag, 15)
    end
  end
  if count == nil or count == "" then
     count = default
  end
  return count
end

-- Standard temple entry header lines.
function temple_entry(e)
  e.tags("temple_entry")
  e.orient("float")
  e.kfeat("O = enter_temple")
end
}}

##############################################################################
# Temple entries
##############################################################################

NAME:   temple_hall
SUBST:  ? : c:20 x v b G:5
: temple_entry(_G)
MAP
ccccccccc
c...O...c
c.......c
c.?...?.c
c.......c
c.......c
c.?...?.c
c.......c
c.......c
c.?...?.c
c.......c
c.......c
c.?...?.c
c.......c
c@..@..@c
ENDMAP

NAME:   temple_hall_moat
TAGS:   no_pool_fixup no_monster_gen
ORIENT: float
WEIGHT: 1
SUBST:  ? : v b G:5
: temple_entry(_G)
MAP
   ccc
   cOc
cccc+cccc
cwwG.Gwwc
cwww.wwwc
cw?w.w?wc
cwww.wwwc
cwww.wwwc
cw?w.w?wc
cwww.wwwc
cwww.wwwc
cw?w.w?wc
cwww.wwwc
cwww.wwwc
cw?w.w?wc
cwww.wwwc
c@..@..@c
ENDMAP

NAME:   temple_fountains
SUBST:  ? : T:30 U
: temple_entry(_G)
MAP
...?...
.......
.?.O.?.
.......
...?...
ENDMAP

NAME:   temple_statues
: temple_entry(_G)
MAP
GOG
ENDMAP

NAME:   temple_green
NSUBST: ? = 1:+ / * = +bb
: temple_entry(_G)
MAP
bbbb+bbbb
b.......b
b.......b
?...O...?
b.......b
b.......b
bbbb?bbbb
ENDMAP

# A set of encased glass and doors: they used to feature secret doors.
# Vault compression here randomly lops off one entrance.
NAME:    temple_doors_ab
WEIGHT:  2
SHUFFLE: Y'"C& / ZD~~~
NSUBST:  XY = 1:+ / * = m+
SUBST:   Z = m, cD : cxxx
SUBST:   ? : ?:40 T:30 U, ? = T:30 U
CLEAR:   ~
: temple_entry(_G)
MAP
cccccccccCC
cccmmmmmcCC
cccm?.?mcCC
@..X.O.Y'"&
cccm?.?mcCC
cccmmmmmcCC
cccccccccCC
ENDMAP

NAME:    temple_doors_cd
WEIGHT:  2
SHUFFLE: Y'"C& / ZD~~~
NSUBST:  XY = 1:+ / * = m+
SUBST:   Z = m, cCD : cxxx
SUBST:   ? : ?:40 T:30 U, ? = T:30 U
CLEAR:   ~
: temple_entry(_G)
MAP
cccccccccCC
cccmmmmmcCC
cccm?.?mcCC
@..X.O.Y'"&
cccm?.?mcCC
cccmmXmmcCC
ccccc.cccCC
ccccc.cccCC
ccccc@cccCC
ENDMAP

NAME:   temple_doors_e
WEIGHT: 6
SUBST:  ? : ?:40 T:30 U, ? = T:30 U
SUBST:  c:cxxx
NSUBST: X = 1:+ / * = m+
: temple_entry(_G)
MAP
cccccc@cccccc
cccccc.cccccc
cccccc.cccccc
ccccmmXmmcccc
ccccm?.?mcccc
@...X.O.X...@
ccccm?.?mcccc
ccccmmXmmcccc
cccccc.cccccc
cccccc.cccccc
cccccc@cccccc
ENDMAP

NAME:    temple_pool_b
TAGS:    no_pool_fixup no_monster_gen no_rotate
: if crawl.x_chance_in_y(3, 10) then
SHUFFLE: XY
SUBST:   X = ., ABCDEFGHIJKLMNPSY = w
: else
SHUFFLE: ABCD / EFGH / IJKL / MNPS
SHUFFLE: AB, EF, IJ, MN
SUBST:   ACD = W, B = wW, EFGHIJKLMNPSXY = w
: end
: temple_entry(_G)
MAP
.................
......wwwww......
....wABwwwwwG....
..wwwCwwwwFHwww..
.wwwwwD...Ewwwww.
.wwwwww.O.YYYYYY.
.wwwwwL...wwwwww.
.wwwwJwwXwSwwwww.
..wwKIwwXwMNwww..
....wwwwXwwPw....
......wwXww......
.................
ENDMAP

# The garden of good and evil
NAME:   temple_garden
TAGS:   no_monster_gen
WEIGHT: 1
SUBST:  _ = 3., - = l.
SUBST:  . = .:900 3:30, ' = .:400 3:30, ! = .:100 3:30
SUBST:  ? = .:50  3:30 G:5 T:10 U:1 V:4
MONS:   angel, rust devil, plant
: temple_entry(_G)
MAP
            ............................xxxxxxxx
        .............'''''...............-llxxxx
      ............'''''''''''...........-llllxxx
    ...........'''''''!!!'''''''........-lll2xxx
   .........''''''''!!!!!!!''''''''......-llllxx
   ........'''''''!!!!!!!!!!!'''''''.......-lllx
  ........'''''!!!!!!!!!!!!!!!!!'''''........llx
 ........'''''!!!!!!!!!?!!!!!!!!!'''''........lx
 .......'''''!!!!!!!!!???!!!!!!!!!'''''........x
.......'''''!!!!!!!!!?????!!!!!!!!!'''''........
.......''''!!!!!!!!!???????!!!!!!!!!''''........
.......'''!!!!!!!!!????O????!!!!!!!!!'''........
.......''''!!!!!!!!!???????!!!!!!!!!''''........
.......'''''!!!!!!!!!?????!!!!!!!!!'''''.......
x.......'''''!!!!!!!!!???!!!!!!!!!'''''........
x........'''''!!!!!!!!!?!!!!!!!!!'''''.........
xx._......'''''!!!!!!!!!!!!!!!!!'''''.........
xxxxm......'''''''!!!!!!!!!!!'''''''..........
xxxxxm......''''''''!!!!!!!''''''''..........
xxxx1m_........'''''''!!!'''''''.............
xxxxxxm_..........'''''''''''...............
xxxxxxxxxx...........'''''................
xxxxxxxxxxxx...........................
ENDMAP

# Firewalks (Lemuel)
# To reach the Temple, you just have to walk straight.
# Autoexclusions and low fire frequency should make this easy,
# and most of the time the safe path is marked anyway.
NAME:    lemuel_temple_firewalk_a
TAGS:    uniq_firewalk no_item_gen no_monster_gen
WEIGHT:  3
SHUFFLE: def
MARKER:  D = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 3, delay = 20, start_clouds = 1, \
             size = 2, walk_dist = 1, spread_rate= 0 }
MARKER:  E = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 4, delay = 39, start_clouds = 1, \
             size = 2, walk_dist = 1, spread_rate= 0 }
MARKER:  F = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 5, delay = 47, start_clouds = 1, \
             size = 2, walk_dist = 1, spread_rate= 0 }
KPROP:   'DEF = no_tele_into
SUBST:   - = --.
COLOUR:  - = yellow
FTILE:   - = floor_pebble_darkgray
: temple_entry(_G)
MAP
xxxxxxxG@Gxxxxxxx
x''''''---''''''x
x''''''---''''''x
x'D'E''---''F'D'x
x''''''---''''''x
x''''''---''''''x
x''''''---''''''x
x'E'F''---''D'E'x
x''''''---''''''x
x''''''---''''''x
x''''''---''''''x
x'F'D''---''E'F'x
x''''''---''''''x
x''''''---''''''x
x''''''---''''''x
x'D'E''---''F'D'x
x''''''---''''''x
x''''''-O-''''''x
xxxxxxxxxxxxxxxxx
ENDMAP

NAME:    lemuel_temple_firewalk_b
TAGS:    uniq_firewalk no_item_gen no_monster_gen
WEIGHT:  7
SHUFFLE: def
MARKER:  D = lua:fog_machine { cloud_type = "flame", \
              pow_min = 2, pow_max = 3, delay = 39, start_clouds = 1, \
              size = 2, walk_dist = 2, spread_rate= 0 }
MARKER:  E = lua:fog_machine { cloud_type = "flame", \
              pow_min = 2, pow_max = 5, delay = 59, start_clouds = 1, \
              size = 2, walk_dist = 2, spread_rate= 0 }
MARKER:  F = lua:fog_machine { cloud_type = "flame", \
              pow_min = 2, pow_max = 7, delay = 79, start_clouds = 1, \
              size = 2, walk_dist = 2, spread_rate= 0 }
KPROP:   'DEF = no_tele_into
SUBST:   - : --.
COLOUR:  - = yellow
FTILE:   - = floor_pebble_darkgray
: temple_entry(_G)
MAP
xxxxxxxG@Gxxxxxxx
x''F'''---''''E'x
x''''''---''''''x
x'D''''---'''F''x
x''''''---''''''x
x''E'''---''''D'x
x''''''---''''''x
x'E''''---'''D''x
x''''''---''''''x
x''F'''---''''E'x
x''''''---''''''x
x'F''''---'''E''x
x''''''---''''''x
x''D'''---''''F'x
x''''''---''''''x
x'D''''---'''F''x
x''''''---''''''x
x''E'''-O-''''D'x
xxxxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_temple_entry_fiery
SUBST:  ' : .'l, ' = .l
SUBST:  l : l w:1
KMASK:  l = no_monster_gen
: temple_entry(_G)
MAP
xxx.@..xx
xxl'...xx
xxll..xxx
xlll'.xxx
xll'.'lxx
xxl.'llxx
xxx'.lxxx
xxxx.xxxx
xccc+cccx
xcG...Gcx
xc.....cx
xc..O..cx
xc.....cx
xcG...Gcx
xcccccccx
xxxxxxxxx
ENDMAP

NAME:   minmay_temple_entry_poolview
TAGS:   no_pool_fixup
SUBST:  ? : lw
SUBST:  c : cx
KMASK:  lw = no_monster_gen
KPROP:  lw = no_tele_into
: temple_entry(_G)
MAP
ccccccccccccc
c???????????c
c???????????c
c???????????c
c???nnnnn???c
c???n...nnnnc
c???n.O.....@
c???n...nnnnc
c???nnnnn???c
c???????????c
c???????????c
c???????????c
ccccccccccccc
ENDMAP

NAME:   minmay_temple_entry_overgrown
MONS:   plant, bush
SUBST:  ? = .1, ! = ...1, 1 : 123, 2 = 2.., 3 = 12
: temple_entry(_G)
MAP
!!!!!
!???!
!?O?!
!???!
!!!!!
ENDMAP

NAME:   minmay_temple_entry_junction
SUBST:  T = TV U:1, X : xc v:1 b:1
: temple_entry(_G)
MAP
xxx...xxx
xxX.T.Xxx
xXX...XXx
.........
.T..O..T.
.........
xXX...XXx
xxX.T.Xxx
xxx...xxx
ENDMAP

NAME:   minmay_temple_entry_glass
SUBST:  m : mb
: temple_entry(_G)
MAP
...........
.m.m.m.m.m.
...........
.m.......m.
...........
.m...O...m.
...........
.m.......m.
...........
.m.m.m.m.m.
...........
ENDMAP

NAME:   minmay_temple_entry_trees
SUBST:  t : t1
MONS:   bush
: temple_entry(_G)
MAP
.....
.t.t.
..O..
.t.t.
.....
ENDMAP

NAME:   minmay_temple_entry_butterflies
MONS:   butterfly
NSUBST: . = 1:1 / 1:1.. / *:.
: temple_entry(_G)
MAP
...
.O.
...
ENDMAP

NAME:   minmay_temple_entry_metal_lava
WEIGHT: 1
KMASK:  l = no_monster_gen
: temple_entry(_G)
MAP
vvvvvvv
vlvlvlv
@..O..@
vlvlvlv
vvvvvvv
ENDMAP

NAME:   minmay_temple_entry_nine_rooms
NSUBST: A = 1:O / * = T:40 VU
NSUBST: B = 1:. / *:c, C = 1:. / *:c, D = 1:. / *:c
NSUBST: E = 1:. / *:c, F = 1:. / *:c
: temple_entry(_G)
MAP
ccBcccBcccBcc
c...c...c...c
B.A.D.A.E.A.B
c...c...c...c
ccDccc.cccEcc
c...c...c...c
B.A...A...A.B
c...c...c...c
ccCccc.cccFcc
c...c...c...c
B.A.C.A.F.A.B
c...c...c...c
ccBcccBcccBcc
ENDMAP

NAME:   minmay_temple_entry_mist
KFEAT:  ! = shallow_water
MARKER: ! = lua:fog_machine { cloud_type = "thin mist", pow_min = 1, \
        pow_max = 3, delay = 10, start_clouds = 1, size_min = 2, \
        size_max = 3, spread_rate = -1 }
KMASK:  W! = no_monster_gen
: temple_entry(_G)
MAP
..WWW..
.WW!WW.
WWWWWWW
W!WOW!W
WWWWWWW
.WW!WW.
..WWW..
ENDMAP

NAME:   minmay_temple_entry_grid
SUBST:  m : bm
: temple_entry(_G)
MAP
.........
...m.m...
..m.m.m..
.m.m.m.m.
..m.O.m..
.m.m.m.m.
..m.m.m..
...m.m...
.........
ENDMAP

NAME:   minmay_temple_entry_hall
SUBST:  c : c:80 xv, T = T:80 UV
: temple_entry(_G)
MAP
ccc+ccc
c.....c
c.T.T.c
+..O..+
c.T.T.c
c.....c
ccc+ccc
ENDMAP

NAME:   minmay_temple_entry_purple_smoke
WEIGHT: 5
MARKER: O = lua:fog_machine { cloud_type = "purple smoke", pow_min = 1, \
        pow_max = 3, delay_min = 1, delay_max = 10, start_clouds = 5,  \
        size_min = 2, size_max = 2, spread_rate = 1 }
: temple_entry(_G)
MAP
.....
.....
..O..
.....
.....
ENDMAP

NAME:   minmay_temple_entry_statues_fountains
: temple_entry(_G)
MAP
.......
.G.G.G.
..T.T..
.G.O.G.
..T.T..
.G.G.G.
.......
ENDMAP

NAME:   minmay_temple_entry_large_hall
SUBST:  x : xxxc, T = T:80 UV
: temple_entry(_G)
MAP
xx@xx@xx@xx
x.........x
@.........@
x..T...T..x
x.........x
@....O....@
x.........x
x..T...T..x
@.........@
x.........x
xx@xx@xx@xx
ENDMAP

NAME:   minmay_temple_entry_glassed_door
: temple_entry(_G)
MAP
xxx+xxx
x.....x
x.m+m.x
+.mOm.+
x.mmm.x
x.....x
xxx+xxx
ENDMAP

NAME:   minmay_temple_entry_steel_oval
WEIGHT: 2
: temple_entry(_G)
MAP
 ....vvv....
...vv...vv...
..v..vvv..v..
.v..v...v..v.
.v.v.vvv.v.v.
v.v.v...v.v.v
v.v.v.O.v.v.v
v.v.v...v.v.v
.v.v.vvv.v.v.
.v..v...v..v.
..v..vvv..v..
...vv...vv...
 ....vvv....
ENDMAP

NAME:   minmay_temple_entry_oklob
WEIGHT: 1
MONS:   plant, oklob sapling
: temple_entry(_G)
MAP
....1..
1.111.1
.11.11.
.1.O211
11.1111
.111.1.
1..11..
ENDMAP

NAME:   minmay_temple_entry_crystal_x
WEIGHT: 5
: temple_entry(_G)
MAP
.......
.b...b.
..b.b..
...O...
..b.b..
.b...b.
.......
ENDMAP

NAME:   minmay_temple_entry_junction_2
WEIGHT: 5
SUBST:  x : xccbbmv, T = T:39 V U:1
: temple_entry(_G)
MAP
...........
...x...x...
...x.T.x...
.xxx...xxx.
...........
..T..O..T..
...........
.xxx...xxx.
...x.T.x...
...x...x...
...........
ENDMAP

NAME:   minmay_temple_entry_water_ring
TAGS:   no_pool_fixup
WEIGHT: 3
SUBST:  x : xxcccvvbt
KMASK:  W = no_monster_gen
: temple_entry(_G)
MAP
.............
.xxx.....xxx.
.xWWWWWWWWWx.
.xWx.....xWx.
..W.......W..
..W.......W..
..W...O...W..
..W.......W..
..W.......W..
.xWx.....xWx.
.xWWWWWWWWWx.
.xxx.....xxx.
.............
ENDMAP

NAME:   minmay_temple_entry_statues
WEIGHT: 5
: temple_entry(_G)
MAP
xGGGx
G...G
G.O.@
G...G
xGGGx
ENDMAP

NAME:   minmay_temple_entry_statues_pillars
SUBST:  c : cccxxb
KFEAT:  O = enter_temple
: temple_entry(_G)
MAP
c.G.c
.....
G.O.G
.....
c.G.c
ENDMAP

NAME:   minmay_temple_entry_from_below_a
WEIGHT: 5
: temple_entry(_G)
MAP
      @
ccccccncc
c.......c
c.O.c.}.c
c.......c
ccncccccc
  @
ENDMAP

NAME:   minmay_temple_entry_from_below_b
WEIGHT: 2
: temple_entry(_G)
MAP
         @
cccccccccnccc
cG.........Gc
c....T.T....c
c..O.....}..c
c....T.T....c
cG.........Gc
cccnccccccccc
   @
ENDMAP

NAME:   minmay_temple_entry_enclosed_plants
SUBST:  t : t12
MONS:   plant, bush
: temple_entry(_G)
MAP
 xxxxx
xxtttxx
xt...tx
xt.O.tx
xt...tx
xxt.txx
 xx+xx
ENDMAP

# Sometimes you just don't feel like walking all the way downstairs, you know?
NAME:   nicolae_temple_entry_ecumenical
TAGS:   uniq_ecumenical_altar
WEIGHT: 1
SUBST:  c : ct, - : p.
KMONS:  p = plant
KFEAT:  _ = altar_ecumenical
: temple_entry(_G)
MAP
 ccccc...ccccc
ccccc-...-ccccc
cc---..T..---cc
c-...........-c
c-._..---..O.-c
c-...-ccc-...-c
cc---ccccc---cc
 ccccccccccccc
ENDMAP

##############################################################################
# Temple maps
##############################################################################
# Maps for the Ecumenial Temple branch are here, through
# PLACE:  Temple
# ORIENT: encompass
#
# Altars are placed as B glyphs, and can (though do not need to) use
# placement modifications with SUBST / SHUFFLE transformations (alongside
# lua conditions or transformations) with
# TAGS:   temple_variable temple_altars_X temple_altars_Z
# : get_altar_count(e, Z)
#
# The temple_altars_[X|Z] tags must correspond to potential altar counts, and
# Z refers to the highest of those given potential altar totals. Layouts should
# stick to the range segments suited for 21 to 24 gods guaranteed in D:
#
# <<1>> Nine to twelve altars
# <<2>> Thirteen to fifteen altars
# <<3>> Sixteen to eighteen altars
# <<4>> Twenty-one altars
# <<5>> Variable altars, generic layouts
# <<6>> Variable altars, unusual layouts
#
# Vaults with fixed altar counts have (a visible) WEIGHT: 10, while vaults
# with variable altar counts add 4 for every range their options fit in.
# Off-beat vaults should halve this (weights 5 and +2), an ambigious criteria:
# consider it for prominent tree / liquid / crystal themes, heavy tile changes,
# particularly compact / chaotic / sprawling layouts, or evoking other vaults
# and non-decorative concepts (such as the temples corrupted by Lugonu or
# slimified by Jiyva).
#
# Gods that don't show up in the Temple due to missing the altar count will
# instead appear in the Dungeon - see des/altar/overflow.des.
#
# For new vault-makers, new layouts should stick to a singular defining shape,
# a fixed altar count and existing themes in some of the other vaults until
# one knows what they are doing. While we have many maps as stands, variety
# in altar count and themes is still welcome.
#
# XXX: For future-proofing and altar count regulating services, these
# solidified ranges could be pre-assessed and weighted in the source to
# some given chances (see the commit history). This way, new layouts don't
# significantly alter altar counts and change only the chances of getting
# other layouts of the same range. This might need some more temple maps in
# the middle ranges if they're weighed up any higher than the most-common
# bottom range, though.

##############################################################################
# <<1>> Nine to twelve gods.

# 10 gods
NAME:    jpeg_temple_alley_garden
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  15
MONS:    bush, plant
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
NSUBST:  . = 12:1 / 7:2 / *:.
COLOUR:  ' : darkgrey w:3 / white w:1 / lightgrey
COLOUR:  . = green
COLOUR:  " = brown
FTILE:   .t12 = floor_grass
FTILE:   'BGQT+ = floor_vault
FTILE:   D" = floor_dirt
MAP
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
tt''''''''''..................................................tt
tt'G'''''G''............................t............t........tt
tt''''''''''....................t..............t..............tt
tt''''''''''.............................................t....tt
tt''''''''''..............ccccccccccccccccccccccccc...........tt
tt''''''''''..............c'''''''''''''''''''''''c....t......tt
tt''''''''''..t.t.t.t.t...c'''Q'''Q'''Q'''Q'''Q'''c...........tt
tt'T'''''T''"""""""""""""Dc'''''''''''''''''''''''c...........tt
tt''''''''''""""""""""""""+'''''''''''''''''''''''c........t..tt
tt''''{'''''""""""""""""""+''''''T'''''''''T''''''c...t.......tt
tt''''''''''""""""""""""""+'''''''''''''''''''''''c...........tt
tt'T'''''T''"""""""""""""Dc'''''''''''''''''''''''c...........tt
tt''''''''''..t.t.t.t.t...c'''Q'''Q'''Q'''Q'''Q'''c.....t.....tt
tt''''''''''..............c'''''''''''''''''''''''c...........tt
tt''''''''''..............ccccccccccccccccccccccccc...........tt
tt''''''''''.........................................t........tt
tt''''''''''....................t...........t.................tt
tt'G'''''G''.......................t.......................t..tt
tt''''''''''..................................................tt
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
ENDMAP

# 9 gods
NAME:    jpeg_temple_island
TAGS:    no_rotate no_pool_fixup
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
MONS:    bush, plant
SUBST:   W = W:30 w, ~ = ':20 W
NSUBST:  ' = 5:t / 7:1 / 4:2 / *:'
NSUBST:  A = 1:{ / *:'
COLOUR:  ' = green / brown w:2
FTILE:   'A12 = floor_grass
FTILE:   .BQT = floor_vault
: set_border_fill_type("open_sea")
MAP
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwWWWWWWWWWWWWWWWWWWWwwwwwwwwwwwwwww
wwwwwwwwwwwwwwWW~~~~~~~~~~~~~~~~~WWwwwwwwwwwwwwww
wwwwwwwwwwwwwWW~'''''''''''''''''~WWwwwwwwwwwwwww
wwwwwwwwwwwwWW~'''''''''''''''''''~WWwwwwwwwwwwww
wwwwwwwwwwwWW~'''''''''''''''''''''~WWwwwwwwwwwww
wwwwwwwwwwWW~''''''''ccccccc''''A'''~WWwwwwwwwwww
wwwwwwwwwWW~'''''''ccc.....ccc'''''''~WWwwwwwwwww
wwwwwwwwWW~''''''ccc...Q.....ccc''''''~WWwwwwwwww
wwwwwwwwW~''''''cc.........Q...cc''''''~Wwwwwwwww
wwwwwwwwW~''''''+...Q........Q..c''''''~Wwwwwwwww
wwwwwwwwW~''A'''+.......Q.T.....c''''''~Wwwwwwwww
wwwwwwwwW~''''''+...Q........Q..c''''''~Wwwwwwwww
wwwwwwwwW~''''''cc.........Q...cc''''''~Wwwwwwwww
wwwwwwwwWW~''''''ccc...Q.....ccc''''''~WWwwwwwwww
wwwwwwwwwWW~'''''''ccc.....ccc'''''''~WWwwwwwwwww
wwwwwwwwwwWW~''''''''ccccccc''''A'''~WWwwwwwwwwww
wwwwwwwwwwwWW~'''''''''''''''''''''~WWwwwwwwwwwww
wwwwwwwwwwwwWW~'''''''''''''''''''~WWwwwwwwwwwwww
wwwwwwwwwwwwwWW~'''''''''''''''''~WWwwwwwwwwwwwww
wwwwwwwwwwwwwwWW~~~~~~~~~~~~~~~~~WWwwwwwwwwwwwwww
wwwwwwwwwwwwwwwWWWWWWWWWWWWWWWWWWWwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
ENDMAP

# 10 gods
NAME:    cyrus_temple_forest
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
MONS:    plant, bush
NSUBST:  . = 10:X / 34:Y / *:.
SUBST:   X : tXY., X = t., Y : Y., Y = 1112..
SUBST:   - = ., " : """.
COLOUR:  .12 = green
COLOUR:  " : brown
COLOUR:  ' : white
FTILE:   T.t12 = floor_grass
FTILE:   " = floor_dirt
FTILE:   '{+GQB = floor_vault
MAP
                 tttttttttttttttttttttttttttttt
                 tttttttttttttttttttttttttttttt
                 tt..........................tt
                 tt.."""................"""..tt
                 tt.."Q"...""".."""....."Q"..tt
                 tt.."""..."Q".."Q"....."""..tt
ccccccccccccccccccc........""".."""..........tt
c'''G''G''G''G''G'c..........................tt
c'''''''''''''''''+--..""".."""..""".."""....tt
c{''''''''''''''''+----"Q".."T".."T".."Q"....tt
c'''''''''''''''''+--..""".."""..""".."""....tt
c'''G''G''G''G''G'c..........................tt
ccccccccccccccccccc..........""".."""........tt
                 tt.."""....."Q".."Q"..."""..tt
                 tt.."Q".....""".."""..."Q"..tt
                 tt.."""................"""..tt
                 tt..........................tt
                 tttttttttttttttttttttttttttttt
                 tttttttttttttttttttttttttttttt
ENDMAP

# 12 gods
NAME:       minmay_temple_forest_path
PLACE:      Temple
ORIENT:     encompass
WEIGHT:     5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
# No plants in corners (confuses autoexplore),
# only fringe the edge of the paths with either (no teleport closets).
KMONS:      P = plant / bush w:1 / fungus w:1
SUBST:      T = t:18 P:1 .:1, U = t:19 .:1
LFLOORCOL:  green
LFLOORTILE: floor_moss
MAP
tttttttttttttttttttttttttttttttttttttttttttttttttt
ttUTTTUttttttttttttttttttttttttttttttttttttttttttt
tUT...TTTUtttttttttUTTTUttttttttttttttttttUTTTUttt
tT.......TTTUttUTTTT...TUttttttttttttttttUT.{.TUtt
tT..Q..T....TUUT........TttttttttttttttttT.....Ttt
tT.....TTTTT.TT..TT..Q..TttttttttttttttttT.....Ttt
tUT...TUtttT....TUT.....TtttttttttUTTTUttT.....Ttt
ttT.TTUttttUT.TTUUT....TUttttttttUT...TUtUT...TUtt
ttUT.TtttttT..TttT.TTTTUttttttttUT.....TttUT.TUttt
tttT.TUtttUT.TTttT.TttttttttttttT...Q..TtttT.Ttttt
tttUT.TttUT...TUUT.TttttttttttttUT.....TttUT.Ttttt
tttUT.TttT.....TT..TtttttttttttttUTT..TUUTT.TUtttt
tttT..TttT..Q..TT.TUttttUTTTUtttttT..TUUT..TUttttt
tttT.TUttT.....TT..TTUtUT...TTTTUUT.TUUT.TTUtttttt
tttT.TUttUT...TTT....TTT........TT...TT.TUtUTTTUtt
tttT..TtttUTTTUUT.TTT.....Q..TTT...TT.T.TtUT...TUt
tttUT.TTUtttttTT.TUtUTTT.....TtUTTTUUT.TUtT.....Tt
tttUT...TUtUTTT.TUtttttT....TUtttttttT.TUtT..Q..Tt
tttT.....TTT...TUttttttT.TTTUttttttttUT.TTT.....Tt
tttT..Q.....TT.TtttttttT.TTTTUttUTTTUtT.T......TUt
tttT.....TTTTT.TttttttUT.....TTTT...TUUT..TTTTTUtt
tttT....TUtT..TUTTTTTTT..TTTT........TTT.TUttttttt
tttT.TTTUttT.TUUT...T...TUttUTTT..Q.....TUtttttttt
tttUT..TUtUT.TtT......TTUttttttT......TTUttttttttt
ttttUT..TTT..TtT..Q..TTUtUTTUttUT...TTUttttttttttt
tttttUT.....TTtT.......TTT..TUttUTT..Ttttttttttttt
ttttttUTTTTTTttUT...TTT...TT.TTTUtUT.TUtttUTTTUttt
ttttttttttttttttT.TTUtUTTTUUT...TUtUT.TTTUT...TUtt
tttttUTTTTTTTTTTT.TtttttttttUTTT.TUTT....T.....Ttt
ttttUT...TT....T..TttttttttttttUT.T..TTT....Q..Ttt
ttttT.......TT...TUttttttUTTTTTTT...TUtUTT.....Ttt
ttttT..Q..TTTTTTTUttttttUT...T....TTUttttUT...TUtt
ttttT.....TtttttttttttttT......TTTUtttttttUTTTUttt
ttttUT...TUtttttttttttttT..Q..TUtttttttttttttttttt
tttttUTTTUttttttttttttttT.....Tttttttttttttttttttt
ttttttttttttttttttttttttUT...TUttttttttttttttttttt
tttttttttttttttttttttttttTTTTTtttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttttttttttttt
ENDMAP

# 11 gods
NAME:   minmay_temple_curvy_loop
TAGS:   no_rotate no_pool_fixup
PLACE:  Temple
WEIGHT: 10
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
ORIENT: encompass
MONS:   plant
SUBST:  y : Ww, z : Ww, Y : W., Z : W.
SUBST:  1 : 1G., X : 1..
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxwwwxxxxxxxxwwwxxxxxxxxxx
xxxxxxxzzzWWWWWxxxxWWWWWyyyxxxxxxx
xxxxx........ZZZxxYY.........xxxxx
xxxx.....................{....xxxx
xxxx............xx............xxxx
xxx............xxxx............xxx
xxx.........xxxxxxxxxx.........xxx
xxx.......xxxxxxxxxxxxxx.......xxx
xx.......xx...xxxxxx...xx.......xx
xx.......x.....xxxx.....x.......xx
x1.......+..Q..xxxx..Q..+.......Xx
xx.......x.....xxxx.....x.......xx
xX.......xx...xxxxxx...xx.......1x
xx.......xxxxxxxxxxxxxxxx.......xx
x1.......xx...xxxxxx...xx.......Xx
xx.......x.....xxxx.....x.......xx
xX.......+..Q..xxxx..Q..+.......1x
xx.......x.....xxxx.....x.......xx
x1.......xx...xxxxxx...xx.......Xx
xx.......xxxxxxxxxxxxxxxx.......xx
xX.......xx...xxxxxx...xx.......1x
xx.......x.....xxxx.....x.......xx
x1.......+..Q..xxxx..Q..+.......Xx
xx.......x.....xxxx.....x.......xx
xX.......xx...xxxxxx...xx.......1x
xx.......xxxxxxxxxxxxxxxx.......xx
x1.......xx...xxxxxx...xx.......Xx
xx.......x.....xxxx.....x.......xx
xX.......+..Q..xxxx..Q..+.......1x
xx.......x.....xxxx.....x.......xx
xx.......xx...xxxxxx...xx.......xx
xxx.......xxxxxxxxxxxxxx.......xxx
xxx.........xxxxxxxxxx.........xxx
xxx............xxxx............xxx
xxxx............xx............xxxx
xxxx..........................xxxx
xxxxx.........YYxxZZZ........xxxxx
xxxxxxxyyyWWWWWxxxxWWWWWzzzxxxxxxx
xxxxxxxxxxwwwxxxxxxxxwwwxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 12 gods
NAME:   minmay_temple_bridge_abc
TAGS:   no_pool_fixup
PLACE:  Temple
ORIENT: encompass
WEIGHT: 10
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SUBST:  A : TUV
: local wrnd = crawl.random2(9)
: if wrnd == 0 then
SUBST:  '"- = ., ;~ = w
: elseif wrnd == 1 then
SUBST:  "; = ., '-b = w, ~ = b
: else
SUBST:  `~ = ., '";b = w
: end
MAP
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwww...wwwwww''''wwwwww...wwwwwwwww
wwwwwwww.....wwww'wwww'wwww..Q..wwwwwwww
wwwwwwww.....www'wwbbww'www.....wwwwwwww
wwwwwwww..A..www'wbbbbw'www.Q.Q.wwwwwwww
wwwwwwww.....www'wwbbww'www.....wwwwwwww
wwwwwwww.....wwww'w;;w'wwww..Q..wwwwwwww
wwwwwwww.....wwwww"ww"wwwww.....wwwwwwww
wwwwwwww......wwww"ww"wwww...Q..wwwwwwww
wwwwwwww........-.-~~-..-.......wwwwwwww
wwwwwwww..{......-~~~~-.......Q.wwwwwwww
wwwwwwww........-.-~~-..-.......wwwwwwww
wwwwwwww......wwww"ww"wwww...Q..wwwwwwww
wwwwwwww.....wwwww"ww"wwwww.....wwwwwwww
wwwwwwww.....wwww'w;;w'wwww..Q..wwwwwwww
wwwwwwww.....www'wwbbww'www.....wwwwwwww
wwwwwwww..A..www'wbbbbw'www.Q.Q.wwwwwwww
wwwwwwww.....www'wwbbww'www.....wwwwwwww
wwwwwwww.....wwww'wwww'wwww..Q..wwwwwwww
wwwwwwwww...wwwwww''''wwwwww...wwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
ENDMAP

# 12 gods
NAME:    nicolae_temple_hex_pool
TAGS:    no_pool_fixup
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: JK, MN
SUBST:   JMP : GTt., KNS : GTt.
NSUBST:  { = 1:{ / *:.
MAP
ccccccccccccccccccccccccccccccc
cccccccccccccc.P.cccccccccccccc
ccccccccccccS.....Scccccccccccc
cccccccccccc.......cccccccccccc
ccccccccccc....{....ccccccccccc
ccccccccccc.........ccccccccccc
ccccccccccc...Q.Q...ccccccccccc
cccc.K.cc.............cc.N.cccc
ccJ.......ww.......ww.......Mcc
cc.......wwwww...wwwww.......cc
c...{.Q...wwwwwwwwwww...Q.{...c
cK........wwwwwwwwwww........Nc
c...Q.....wwwwwwwwwww.....Q...c
cc.......wwwwwwwwwwwww.......cc
cc.......wwwwwwwwwwwww.......cc
cccc...wwwwwwwwwwwwwwwww...cccc
ccccc.wwwwwwwwwwwwwwwwwww.ccccc
cccc...wwwwwwwwwwwwwwwww...cccc
cc.......wwwwwwwwwwwww.......cc
cc.......wwwwwwwwwwwww.......cc
c...Q.....wwwwwwwwwww.....Q...c
cN........wwwwwwwwwww........Sc
c...{.Q...wwwwwwwwwww...Q.{...c
cc.......wwwww...wwwww.......cc
ccM.......ww.......ww.......Pcc
cccc.N.cc.............cc.S.cccc
ccccccccccc...Q.Q...ccccccccccc
ccccccccccc.........ccccccccccc
ccccccccccc....{....ccccccccccc
cccccccccccc.......cccccccccccc
ccccccccccccK.....Kcccccccccccc
cccccccccccccc.J.cccccccccccccc
ccccccccccccccccccccccccccccccc
ENDMAP

# 9 gods
NAME:    nicolae_temple_figure_zero
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: Cc / Dd
SUBST:   cD = Q, Cd = .
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvv
vvvvvvvvvvvvv...vvvvvvvvvvvvv
vvvvvvvvvvvv.....vvvvvvvvvvvv
vvvvvvvvvvvv.CcC.vvvvvvvvvvvv
vvvvv...vvvv.....vvvv...vvvvv
vvvv.....vvvv...vvvv.D...vvvv
vvvv..Q..vvvvv+vvvvv..d..vvvv
vvvv.....vvv.....vvv...D.vvvv
vvvvv...+v.........v+...vvvvv
vvvvvvvvv...........vvvvvvvvv
vvvvvvvv.....vvv.....vvvvvvvv
vvvvvvvv....vvvvv....vvvvvvvv
vv...vv....v+...+v....vv...vv
v.....v...vv.....vv...v.....v
v..Q..+...vv..{..vv...+..Q..v
v.....v...vv.....vv...v.....v
vv...vv....v+...+v....vv...vv
vvvvvvvv....vvvvv....vvvvvvvv
vvvvvvvv.....vvv.....vvvvvvvv
vvvvvvvvv...........vvvvvvvvv
vvvvv...+v.........v+...vvvvv
vvvv.....vvv.....vvv.....vvvv
vvvv..Q..vvvvv+vvvvv..Q..vvvv
vvvv.....vvvv...vvvv.....vvvv
vvvvv...vvvv.....vvvv...vvvvv
vvvvvvvvvvvv..Q..vvvvvvvvvvvv
vvvvvvvvvvvv.....vvvvvvvvvvvv
vvvvvvvvvvvvv...vvvvvvvvvvvvv
vvvvvvvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP

##############################################################################
# <<2>> Thirteen to fifteen gods.

# 13 gods
NAME:    dshaligram_water_room_temple
TAGS:    no_pool_fixup no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
MAP
ccccccccccccccccccccccccccccccccccccccccccccccccccc
cwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwc
cwwwwwwwwwwww.........................wwwwwwwwwwwwc
cwwwwwwww.................................wwwwwwwwc
cwwwww...................G...................wwwwwc
cwww......Q.............................Q......wwwc
cwww.............Q...............Q.............wwwc
cww.............................................wwc
cww.............................................wwc
cww...Q......Q...........{...........Q......Q...wwc
cww.............................................wwc
cww.............................................wwc
cwww.............Q...............Q.............wwwc
cwww......Q.............................Q......wwwc
cwwwww...................Q...................wwwwwc
cwwwwwwww.................................wwwwwwwwc
cwwwwwwwwwwww.........................wwwwwwwwwwwwc
cwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwc
ccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

# 14 gods
NAME:    eino_temple_water
TAGS:    no_pool_fixup no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: set_border_fill_type("open_sea")
MAP
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwww...wwwwwwwwwwwwwww
wwwwwwwwwwwwww..Q..wwwwwwwwwwwwww
wwwwwwwwwwww.........wwwwwwwwwwww
wwwwwwwwwwww....Q....wwwwwwwwwwww
wwwwwwwwwww..Q.....Q..wwwwwwwwwww
wwwwwwwwwwww.........wwwwwwwwwwww
wwwwwwwwwwww...www...wwwwwwwwwwww
wwwwwwwwwww...wwwww...wwwwwwwwwww
wwwwwwwwww...ww...ww...wwwwwwwwww
wwwwwwwwww......Q......wwwwwwwwww
wwwwwwwww..Q.........Q..wwwwwwwww
wwwwwwwwww...www.......wwwwwwwwww
wwwwwwwwwwwwwwwww...wwwwwwwwwwwww
wwwwwwwwwwwwwwwwww...wwwwwwwwwwww
wwwwwwwwwwwwwwwwwww...wwwwwwwwwww
wwwwwwwwwwwwwwwwwww...wwwwwwwwwww
wwwwwwwwwwwwwwwwww...wwwwwwwwwwww
wwwwwwwwwwwwwwwww...wwwwwwwwwwwww
wwwwwwwwwww...........wwwwwwwwwww
wwwwwwwwww..G...{...G..wwwwwwwwww
wwwwwwwwwww...........wwwwwwwwwww
wwwwwwwwwwwww...wwwwwwwwwwwwwwwww
wwwwwwwwwwww...wwwwwwwwwwwwwwwwww
wwwwwwwwwww...wwwwwwwwwwwwwwwwwww
wwwwwwwwwww...wwwwwwwwwwwwwwwwwww
wwwwwwwwwwww...wwwwwwwwwwwwwwwwww
wwwwwwwwwwwww...wwwwwwwwwwwwwwwww
wwwwwwwwww.......www...wwwwwwwwww
wwwwwwwww..Q.........Q..wwwwwwwww
wwwwwwwwww......Q......wwwwwwwwww
wwwwwwwwww...ww...ww...wwwwwwwwww
wwwwwwwwwww...wwwww...wwwwwwwwwww
wwwwwwwwwwww...www...wwwwwwwwwwww
wwwwwwwwwwww.........wwwwwwwwwwww
wwwwwwwwwwww.Q.....Q.wwwwwwwwwwww
wwwwwwwwwww.....Q.....wwwwwwwwwww
wwwwwwwwwwww.........wwwwwwwwwwww
wwwwwwwwwwwwww..Q..wwwwwwwwwwwwww
wwwwwwwwwwwwwww...wwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
ENDMAP

# 13 gods
NAME:    cyrus_circular_temple
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SUBST:   X : xc.
MAP
         ccccccccccccc
      cccc...........cccc
    ccc........{........ccc
   cc.....................cc
  cc.....Q...X...X...Q.....cc
 cc.........................cc
 c.........X...Q...X.........c
cc...........................cc
c........Q...X...X...Q........c
c.............................c
c..Q...X...X...Q...X...X...Q..c
c.............................c
c........Q...X...X...Q........c
cc...........................cc
 c.........X...Q...X.........c
 cc.........................cc
  cc.....Q...X...X...Q.....cc
   cc.....................cc
    ccc........T........ccc
      cccc...........cccc
         ccccccccccccc
ENDMAP

# 14 gods
NAME:    minmay_temple_curvy_branch
PLACE:   Temple
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
ORIENT:  encompass
SHUFFLE: GT, C'D" / D"C' / C'E` / E`C'
SUBST:   C = Q, '"D = ., E` = x
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx...xxxxxxxx
xxxxxxx.....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxxxx
xxxxxxx..Q..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..Q..xxxxxxx
xxxxxxx.....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxxxx
xxxxxxxx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx...xxxxxxxx
xxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx+xxxxxxxxx
xx...xxx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx...xxx...xx
x.....x........xxxxxxxxxxxxxxxxxxxxxxxxx........x.....x
x..Q..+..G........xxxxxxxxxxxxxxxxxxx........G..+..Q..x
x.....x.............xxxxxx"""xxxxxx.............x.....x
xx...xxx.............xxxx"""""xxxx.............xxx...xx
xxxxxxxxx+xxxx.......xxxx""D""xxxx.......xxxx+xxxxxxxxx
xxxxxxxx...xxxxxx.....xxx"""""xxx.....xxxxxx...xxxxxxxx
xxxxxxx.....xxxxxxx....xxx"""xxx....xxxxxxx.....xxxxxxx
xxxxxxx..Q..xxxxxxxx.....xx+xx.....xxxxxxxx..Q..xxxxxxx
xxxxxxx.....xxxxxxxx...............xxxxxxxx.....xxxxxxx
xxxxxxxx...xxxxx'''xx....T...T....xx'''xxxxx...xxxxxxxx
xxxxxxxxxxxxxxx'''''x.............x'''''xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx''C''+......{......+''C''xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx'''''x.............x'''''xxxxxxxxxxxxxxx
xxxxxxxx...xxxxx'''xx....T...T....xx'''xxxxx...xxxxxxxx
xxxxxxx.....xxxxxxxx...............xxxxxxxx.....xxxxxxx
xxxxxxx..Q..xxxxxxxx.....xx+xx.....xxxxxxxx..Q..xxxxxxx
xxxxxxx.....xxxxxxx....xxxx"xxxx....xxxxxxx.....xxxxxxx
xxxxxxxx...xxxxxx.....xxxxx"xxxxx.....xxxxxx...xxxxxxxx
xxxxxxxxx+xxxx.......xxxxxx"xxxxxx.......xxxx+xxxxxxxxx
xx...xxx.............xxxxxx"xxxxxx.............xxx...xx
x.....x.............xxxxxxx"xxxxxxx.............x.....x
x..Q..+..G........xxxxxxxxx"xxxxxxxxx........G..+..Q..x
x.....x........xxxxxxxxxxx"""xxxxxxxxxxx........x.....x
xx...xxx...xxxxxxxxxxxxxx"""""xxxxxxxxxxxxxx...xxx...xx
xxxxxxxxx+xxxxxxxxxxxxxxx""D""xxxxxxxxxxxxxxx+xxxxxxxxx
xxxxxxxx...xxxxxxxxxxxxxx"""""xxxxxxxxxxxxxx...xxxxxxxx
xxxxxxx.....xxxxxxxxxxxxxx"""xxxxxxxxxxxxxx.....xxxxxxx
xxxxxxx..Q..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..Q..xxxxxxx
xxxxxxx.....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxxxx
xxxxxxxx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx...xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 13 altars
NAME:   oiseaux_autumnal_temple
TAGS:   no_rotate no_pool_fixup
PLACE:  Temple
ORIENT: encompass
WEIGHT: 5
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KFEAT:  A = w
SUBST:  c : c-""""""", " = c-
MARKER: A = lua:fog_machine { cloud_type = "thin mist", pow_min = 2, \
        pow_max = 6, delay = 4, start_clouds = 1, size_min = 2, \
        size_max = 6, spread_rate = 2 }
COLOUR: . = brown
COLOUR: t = red w:5 / yellow w:5 / brown
COLOUR: ' = darkgrey
FTILE:  t. = floor_swamp
FTILE:  - = floor_pebble
FTILE:  Q{' = floor_pebble_brown
TILE:   c = wall_stone_smooth
TILE:   t = dngn_tree_yellow w:5 / dngn_tree_lightred w:5 / \
            dngn_tree_red / dngn_tree_dead w:3
MAP
tttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttt
ttttttttttAwwwtttttttttttttttttttttttttt
tttttttttttttwwwtttttttttttttttttttttttt
tttttttttttttttwwwtttttttttttttttttttttt
ttttt.-ccccttttttwwwtttttttttttttttttttt
ttttt.c'''cttttttttwwttttttttttttttttttt
ttttt.c'Q'ctttttttttwwtttttttttttttttttt
tttt..c'''ttttttttttwwtttttttttttttttttt
ttttttc-c''t..tttttwwtttttttcccctttttttt
ttttttt...''.--tttWwwtttttttc''''ttttttt
tttttttt..c'''c..tWwwWttttttc'Q'tttttttt
tttttt....-'Q'c...WwwW..ttt.-.''''.ttttt
ttttt......'''c..WwwW'--..t.c.'''..ttttt
tttt........''..Wwwc'''c'......'....tttt
tt...cc-c-...'tWwwWc'Q''.....''....ttttt
ttt..c'''c.....WwW.-''.......'''..tttttt
ttt..c'Q'''.'..WwwWt....t...-'Q'-..ttttt
tttt.c''.c......Wwwtt........''.cttttttt
ttttttt......t..ttwttt....'.'..ttttttttt
ttttttttttt...ttttwwttt..t.......ttttttt
tttttttttttt..tttttwttttt..-cc'...tttttt
ttttttttttt'.ttttttwwtttt..''c.....ttttt
ttttttt.ccc''...ttttwwwttt'Q'c......tttt
tttttt..-'''''.ttcttttwwtt'''-.....ttttt
ttt.....c''Q''c..c'Qtttwwt.....'.--.tttt
tttt....c'''''c..c'''ttWwwW.....''c..ttt
ttt.......'''....-cc'cttWwW.--.'Q't...tt
tt..--.....''......'''.tWwwW--..''c..ttt
tt..c''.....'t.....'''''tWww--W..cc...tt
tt..t'Q'.....'.........'.tWw--wW.....ttt
ttt..'''.........t.........W--wwW...tttt
tt...c...t............t....'--WwwW...ttt
ttttt...'...t....''....t....--tWwwW..ttt
tttttttt..'ttt..''''.tttt.....ttWwwWtttt
ttttttttt'.ttttt-'Q''.ttttt..'ttttwttttt
tttttttt''ttttttc'''.ttttttt'.ttttwwtttt
ttttttt''tttttttccc-ttttttttt''ttttwwttt
ttttttc-'ttttttttttttttttttt''tttttwwttt
ttttttc'''ctttttttttttttttttt''''tttwwtt
ttttttc'Q'cttttttttttttttttt''''''tttwtt
ttttttc'''c.ttttttttttttttt'''{'''tttAtt
tttttt-cccc.tttttttttttttttt''''''tttttt
ttttttt.....tttttttttttttttttt''tttttttt
tttttttttt.ttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttt
ENDMAP

# 13 gods
NAME:   nicolae_temple_figure_eight
PLACE:  Temple
WEIGHT: 10
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
ORIENT: encompass
NSUBST: A = 1:{ / *:Q
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
vvvvvvvvvvvvv...vvvvvvvvvvvvv...vvvvvvvvvvvvv
vvvvvvvvvvvv.....vvvvvvvvvvv.....vvvvvvvvvvvv
vvvvvvvvvvvv..Q..vvvvvvvvvvv..Q..vvvvvvvvvvvv
vvvvv...vvvv.....vvvv...vvvv.....vvvv...vvvvv
vvvv.....vvvv...vvvv.....vvvv...vvvv.....vvvv
vvvv..Q..vvvvv+vvvvv..A..vvvvv+vvvvv..Q..vvvv
vvvv.....vvv.....vvv.....vvv.....vvv.....vvvv
vvvvv...+v.........v+...+v.........v+...vvvvv
vvvvvvvvv...........vvvvv...........vvvvvvvvv
vvvvvvvv.....vvv.....vvv.....vvv.....vvvvvvvv
vvvvvvvv....vvvvv.....v.....vvvvv....vvvvvvvv
vv...vv....v+...+v.........v+...+v....vv...vv
v.....v...vv.....vv.......vv.....vv...v.....v
v..A..+...vv..A..vvv.....vvv..A..vv...+..A..v
v.....v...vv.....vv.......vv.....vv...v.....v
vv...vv....v+...+v.........v+...+v....vv...vv
vvvvvvvv....vvvvv.....v.....vvvvv....vvvvvvvv
vvvvvvvv.....vvv.....vvv.....vvv.....vvvvvvvv
vvvvvvvvv...........vvvvv...........vvvvvvvvv
vvvvv...+v.........v+...+v.........v+...vvvvv
vvvv.....vvv.....vvv.....vvv.....vvv.....vvvv
vvvv..Q..vvvvv+vvvvv..A..vvvvv+vvvvv..Q..vvvv
vvvv.....vvvv...vvvv.....vvvv...vvvv.....vvvv
vvvvv...vvvv.....vvvv...vvvv.....vvvv...vvvvv
vvvvvvvvvvvv..Q..vvvvvvvvvvv..Q..vvvvvvvvvvvv
vvvvvvvvvvvv.....vvvvvvvvvvv.....vvvvvvvvvvvv
vvvvvvvvvvvvv...vvvvvvvvvvvvv...vvvvvvvvvvvvv
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP

# 13 gods
NAME:    nicolae_temple_god_jail
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KFEAT:   m = iron_grate
SHUFFLE: PSR
SUBST:   PS = m, R = +
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvv
v...v...v...v...v...v...v...v
v.Q.v.Q.v.Q.v.{.v.Q.v.Q.v.Q.v
v...v...v...v...v...v...v...v
vPSRvPSRvPSRv...vRSPvRSPvRSPv
v...........m...m...........v
v...........+...+...........v
v...........m...m...........v
vPSRvPSRvPSRvm+mvRSPvRSPvRSPv
v...v...v...v...v...v...v...v
v.Q.v.Q.v.Q.v.Q.v.Q.v.Q.v.Q.v
v...v...v...v...v...v...v...v
vvvvvvvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP

# 14 gods
NAME:    nicolae_temple_great_sept
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
# Sadly, console doesn't have colors for orange and indigo,
# so brown and cyan will have to do.
KFEAT:   roygbiv = n
KFEAT:   ' = .
SHUFFLE: roygbiv / vroygbi / ivroygb / bivroyg / gbivroy / ygbivro / oygbivr
SUBST:   T = 't
FTILE:   . = floor_hall, t' = floor_grass, !{Q = floor_limestone
COLOUR:  r = red, o = brown, y = yellow, g = green, b = blue, i = cyan, \
         v = magenta, ' = green, . = blue, ! = lightgrey
TILE:    r = dngn_transparent_stone_red, o = dngn_transparent_stone_brown, \
         y = dngn_transparent_stone_yellow, g = dngn_transparent_stone_green, \
         b = dngn_transparent_stone_blue, i = dngn_transparent_stone, \
         v = dngn_transparent_stone_magenta
KPROP:   't = no_tele_into
MAP
ttttttttttttttttttttttttttttttttttttttttttttt
ttttttttttttttttttttttttttttttttttttttttttttt
tttttttttttttttTTTTTTTTTTTTTTTttttttttttttttt
ttttttttttttttT'''''''''''''''Ttttttttttttttt
tttttttttttttT'''''''''''''''''Tttttttttttttt
ttttttttttttT'''''''''''''''''''Ttttttttttttt
tttttttTTTTTT'''''''''''''''''''TTTTTTttttttt
ttttttT''''''''''''rrrrrrr''''''''''''Ttttttt
tttttT''''''''''''cc.....cc''''''''''''Tttttt
ttttT''''''''''''cc.......cc''''''''''''Ttttt
ttttT''''''''''''c...!!!...c''''''''''''Ttttt
ttttT''''''vvccccc..!!!!!..cccccoo''''''Ttttt
ttttT'''''vv........!Q!Q!........oo'''''Ttttt
ttttT''''vv.........!!!!!.........oo''''Ttttt
ttttT''''v....!!!....!!!....!!!....o''''Ttttt
ttttT''''c...!!Q!...........!Q!!...c''''Ttttt
tttT'''''c..!!!!!...........!!!!!..c'''''Tttt
ttT''''''c..!Q!!.............!!Q!..c''''''Ttt
ttT''''''c..!!!...............!!!..c''''''Ttt
ttT''''''cc.......................cc''''''Ttt
ttT'''''cc...........!!!...........cc'''''Ttt
ttT''''cc...........!!!!!...........cc''''Ttt
ttT''''i..!!!.......!!{!!.......!!!..y''''Ttt
ttT''''i..!Q!!......!!!!!......!!Q!..y''''Ttt
ttT''''i..!!!!!......!!!......!!!!!..y''''Ttt
ttT''''i...!!Q!...............!Q!!...y''''Ttt
ttT''''i....!!!...............!!!....y''''Ttt
ttT''''ii...........................yy''''Ttt
ttT'''''cc.........................cc'''''Ttt
ttT''''''ccccc...!!!.....!!!...ccccc''''''Ttt
ttT''''''''''c..!!!!!...!!!!!..c''''''''''Ttt
ttT''''''''''c..!Q!Q!...!Q!Q!..c''''''''''Ttt
tttT'''''''''c..!!!!!...!!!!!..c'''''''''Tttt
ttttT''''''''c...!!!.....!!!...c''''''''Ttttt
tttttTTTT''''bb.......c.......gg''''TTTTttttt
ttttttttT'''''bb.....ccc.....gg'''''Ttttttttt
ttttttttT''''''bbbcccc'ccccggg''''''Ttttttttt
ttttttttT'''''''''''''''''''''''''''Ttttttttt
ttttttttT'''''''''''''''''''''''''''Ttttttttt
tttttttttT'''''''''''''''''''''''''Tttttttttt
ttttttttttT'''''''''''''''''''''''Ttttttttttt
tttttttttttTTTTTTTTTTTTTTTTTTTTTTTttttttttttt
ttttttttttttttttttttttttttttttttttttttttttttt
ttttttttttttttttttttttttttttttttttttttttttttt
ENDMAP

# 15 gods
NAME:    regret_index_temple_waves
TAGS:    no_rotate no_pool_fixup
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: {<defgh / {<defgh / <{hgfed
SHUFFLE: def, PMRS, -' / -' / ="
SUBST:   < = Q, d : W.., e : W.ccc, fgh = c
SUBST:   P : wc, M : wc.., R : wc..., S : wc....
SUBST:   - = ~, ' = ., =" = c, ~ : cccww
MAP
ccccccccccccccccccccccccccc
c--~~~~~~~~~~~~~~~~.....''c
c--~~~~~~~~~~~~~~~~.....''c
c--~~~~~~~~~~~~.......{.''c
c--~~~~~~~~~~......P....''c
c--~~~~~~.......Q.......''c
c--~~~~......M......cchcccc
c''.......Q.......ccc...''c
c''....P......ccgcc.....''c
c''.<.......cfc.......Q.''c
c''.....ccccc......R....''c
c''...cec.......Q.......''c
ccccdcc......S......ccdcccc
c''.......Q.......cec...''c
c''....R......ccccc.....''c
c''.Q.......cfc.......Q.''c
c''.....ccccc......R....''c
c''...cgc.......Q.......''c
cccchcc......S......cchcccc
c''.......Q.......cgc...''c
c''....R......ccccc.....''c
c''.Q.......cfc.......Q.''c
c''.....ccccc......P....''c
c''...cec.......Q.......''c
ccccdcc......M......~~~~--c
c''.......Q.......~~~~~~--c
c''....P......~~~~~~~~~~--c
c''.Q.......~~~~~~~~~~~~--c
c''.....~~~~~~~~~~~~~~~~--c
c''...~~~~~~~~~~~~~~~~~~--c
ccccccccccccccccccccccccccc
ENDMAP

# 15 gods
NAME:    regret_index_temple_generic_petals
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KMONS:   P = plant
KMONS:   b = bush
SHUFFLE: de, fg, DEFGHI
SUBST:   d : ++., f : ++., eg = c
SUBST:   h : ++., i : ++., j : ++., k : ++.
SUBST:   D : Pbx.., E : Pbx..., F : Pxx...., G : Pxx.....
SUBST:   H : Pxx......, I : Pxx......., ' : c.., " : c..
MAP
ccccccccccccccccccccccccccccccccc
c........c...'''.'''...c........c
c.D......d....'...'....f...Q..E.c
c...{....c.....'.'.....c........c
c.......cc.............cc.....Q.c
c......cc......chc......cc......c
c....cec......cc.cc......cgc....c
c...cc.....cccc...cccc.....cc...c
ccdcc....kcc.cc...cc.cci....ccfcc
c........c...c..Q..c...c........c
c.......cc.Q.cc...cc.Q.cc.......c
c.......c...I.c.F.c.G...c.......c
c"......cccc..cc.cc..cccc......"c
c""....ccc.ccccccccccc.ccc....""c
c"."..cc.....ccccccc.....cc.."."c
c...".j...Q.H.ccccc.H.Q...j."...c
c"."..cc.....ccccccc.....cc.."."c
c""....ccc.ccccccccccc.ccc....""c
c"......cccc..cc.cc..cccc......"c
c.......c...G.c.F.c.I...c.......c
c.......cc.Q.cc...cc.Q.cc.......c
c........c...c..Q..c...c........c
ccfcc....icc.cc...cc.cck....ccdcc
c...cc.....cccc...cccc.....cc...c
c....cgc......cc.cc......cec....c
c......cc......chc......cc......c
c.Q.....cc.............cc..Q..Q.c
c........c.....'.'.....c........c
c.E..Q...f....'...'....d...Q..D.c
c........c...'''.'''...c........c
ccccccccccccccccccccccccccccccccc
ENDMAP

##############################################################################
# <<3>> Sixteen to eighteen gods.

# 18 gods
NAME:    dpeg_five_rooms_temple
TAGS:    no_rotate
PLACE:   Temple
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
ORIENT:  encompass
SUBST:   X : GUT
MAP
                   xxxxxxxxx
              xxxxxx.......xxxxxx
         xxxxxx........x........xxxxxx
      xxxx........xxxxx+xxxxx........xxxx
     xx.....xxxxxxxxx.....xxxxxxxxx.....xx
    xx...xxxxxxxxxx.........xxxxxxxxxx...xx
   xx..xxxxxxxxxxx...........xxxxxxxxxxx..xx
  xx..xxxxxxxxxxxx.....{.....xxxxxxxxxxxx..xx
  x..xxxxxxxxxxxxx...........xxxxxxxxxxxxx..x
 xx..xxx.....xxxxxx.........xxxxxx.....xxx..xx
 x..+x...Q.....xxxxxx.....xxxxxx.....Q...x+..x
xx..x...........xxxxxxxxxxxxxxx...........x..xx
x..xx..Q..X..Q..xxxxxxxxxxxxxxx..Q..X..Q..xx..x
x..xx...........xxxxxxxxxxxxxxx...........xx..x
x..xxx.....Q...xxxxxxxxxxxxxxxxx...Q.....xxx..x
x..xxxxx.....xxxxxxxxxxxxxxxxxxxxx.....xxxxx..x
x..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..x
x..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..x
x..xxxxxxxxxx.....xxxxxxxxxxx.....xxxxxxxxxx..x
xx..xxxxxxx...Q.....xxxxxxx.....Q...xxxxxxx..xx
 x..xxxxxx.......Q...xxxxx...Q.......xxxxxx..x
 xx..xxxxx..Q..X.....xxxxx.....X..Q..xxxxx..xx
  x..xxxxx........Q..xxxxx..Q........xxxxx..x
  xx..xxxxx....Q....xxxxxxx....Q....xxxxx..xx
   xx..xxxxxx.....xxxxxxxxxxx.....xxxxxx..xx
    xx...xxx+xxxxxxxxxxxxxxxxxxxxx+xxx...xx
     xxx....xxxxxxxxxxxxxxxxxxxxxxx....xxx
       xxx........xxxxxxxxxxx........xxx
         xxxxxx........x........xxxxxx
              xxxxxx.......xxxxxx
                   xxxxxxxxx
ENDMAP

# 16 gods
NAME:    eino_temple_scatter_basement
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: CD, ab, cde, fgh, ij, klm, no, pq, rs, tu, vw, yz, @&
SUBST:   C = Q, D = ., a : x., c : x., f : x., i : x., k : x.
SUBST:   n : x., p : x., r : x., t : x., v : x., y : x., @ : x.
SUBST:   bdeghjlmoqsuwz& = x, ' : x., " : x., ` : x.
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx@xxxxxxxxxxx&xxxxxxxxxxx
xxxxvx...x...x.xyxzx.x...x...xwxxxx
xxxvx.....x.x...xxx...x.x.....xwxxx
xxxx....C..x..D..x..D..x..C....xxxx
xxxt............xxx............uxxx
xxxx...x.x....xxrxsxx....x.x...xxxx
xnxxx.xxxxx..xpxxxxxqx..xxxxx.xxxox
x..x...xxx....xxlxmxx....xxx...x..x
x.x.....x.......xkx.......x.....x.x
xx...Q..x..Q.....x.....Q..x..Q...xx
x.......x.................x.......x
xx.....xxx....`..'.."....xxx.....xx
xix...xdxex...xx...xx...xgxhx...xjx
xx.....xcx...xaxx.xxbx...xfx.....xx
x.......x.....xx...xx.....x.......x
x..Q.......x.Qx..{..xQ.x.......Q..x
x.......x.....xx...xx.....x.......x
xx.....xfx...xbxx.xxax...xcx.....xx
xjx...xhxgx...xx...xx...xexdx...xix
xx.....xxx...."..'..`....xxx.....xx
x.......x.................x.......x
xx...Q..x..Q.....x.....Q..x..Q...xx
x.x.....x.......xkx.......x.....x.x
x..x...xxx....xxmxlxx....xxx...x..x
xoxxx.xxxxx..xqxxxxxpx..xxxxx.xxxnx
xxxx...x.x....xxsxrxx....x.x...xxxx
xxxu............xxx............txxx
xxxx....C..x..D..x..D..x..C....xxxx
xxxwx.....x.x...xxx...x.x.....xvxxx
xxxxwx...x...x.xzxyx.x...x...xvxxxx
xxxxxxxxxxx&xxxxxxxxxxx@xxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 16 gods
NAME:    eino_temple_rhombus
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: defghij, klmnopq, rs / rs / sr, tu / tu / ut
SUBST:   deklrt = x, f : x.., m : x.., ghijnopqsu = .
SUBST:   W : xx., X : xx., Y : xx., Z : xx.
SUBST:   D : GGTTUb.., E : GGTTUb.., F : GGTTUb..
MAP
                    xxx       xxx
                   xxXxx     xxYxx
                  xx...xx   xx...xx
                 xx..x..xx xx..x..xx
                xx..x.x..xxx..x.x..xx
               xx..x...x..x..x...x..xx
              xx..x..Q..x.F.x..Q..x..xx
             xx....r...x.....x...t....xx
            xx..x.ssr.x...x...x.tuu.x..xx
           xx..x.rss.x...x.x...x.uut.x..xx
          xx..x...r.....x.A.x.....t...x..xx
          xW.x..Q..x...x.....x...x..Q..x.Zx
          xx..x...x...x...Q...x...x...x..xx
           xx..x.x...x.de...lk.x...x.x..xx
            xx..x...x..fgh.onm..x...x..xx
             xx....x....ij.qp....x....xx
              xxE.x...Q...D...Q...x..xx
             xx....x....pq.ji....x..xx
            xx..x...x..mno.hgf..x..xx
           xx..x.x...x.kl...ed.x..xx
          xx..x...x...x...Q...x..xx
         xx..x.....x.{.x.....x..xx
        xx..x...Q...x...x...x..xx
       xx..x.de...lk.x...x.x..xx
      xx..x..fgh.onm..x...x..xx
     xx..x....ij.qp....x....xx
    xx..x...Q...D...Q...x.Exx
   xx....x....pq.ji....x....xx
  xx..x...x..mno.hgf..x...x..xx
 xx..x.x...x.kl...ed.x...x.x..xx
xx..x...x...x...Q...x...x...x..xx
xZ.x..Q..x...x.....x...x..Q..x.Wx
xx..x...t.....x...x.....r...x..xx
 xx..x.tuu.x...x.x...x.ssr.x..xx
  xx..x.ttt.x...x...x.rss.x..xx
   xx....t...x.....x...r....xx
    xx..x..Q..x.F.x..Q..x..xx
     xx..x...x..x..x...x..xx
      xx..x.x..xxx..x.x..xx
       xx..x..xx xx..x..xx
        xx...xx   xx...xx
         xxYxx     xxXxx
          xxx       xxx
ENDMAP

# 17 gods
# XXX: Too large and enclosed?
NAME:    minmay_crystal_spiral_temple
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
MAP
                  bbbbbbb
                  b.....b
                  b.....b
                  b..{..b
                 bb.....b
                bb......b
            bbbbb...bbbbbbbbbbb
            b......bb   b.....b
            b.....bb    b.....b
            b..Q..b     b..Q..b
           bb.....b    bb.....bb
          bb......b   bb.......bb
      bbbbb...bbbbbbbbb...bbb...bbbbb
      b......bb   b......bb bb......b
      b.....bb    b.....bb   bb.....b
      b..Q..b     b..Q..b     b..Q..b
     bb.....b    bb.....b     b.....bb
    bb......b   bb......b     b......bb
bbbbb...bbbbbbbbb...bbbbbbbbbbbbbbb...bbbbb
b......bb   b......bb   b.....b   bb......b
b.....bb    b.....bb    b...Q.b    bb.....b
b..Q..b     b..Q..b     b.....b     b..Q..b
b.....bb    b.....bb   bb.Q...b    bb.....b
b......bb   b......bb bb......b   bb......b
bbbbb...bbbbbbbbb...bbb...bbbbbbbbb...bbbbb
    bb......b   bb.......bb   b......bb
     bb.....b    bb.....bb    b.....bb
      b..Q..b     b..Q..b     b..Q..b
      b.....bb    b.....b    bb.....b
      b......bb   b.....b   bb......b
      bbbbb...bbbbbbbbbbbbbbb...bbbbb
          bb......b     b......bb
           bb.....b     b.....bb
            b..Q..b     b..Q..b
            b.....bb   bb.....b
            b......bb bb......b
            bbbbb...bbb...bbbbb
                bb.......bb
                 bb...Q.bb
                  b.....b
                  b.Q...b
                  b.....b
                  bbbbbbb
ENDMAP

# 16 gods
NAME:    nicolae_temple_elliptical
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: G{
NSUBST:  { = 1:{ / *:G
SUBST:   G : GT, c : ccccvb, d : cccvbxm
MAP
                 ccccccccccccccc
             ccccc.............ccccc
          cccc..........{..........cccc
        ccc...........................ccc
      ccc.............dd.dd.............ccc
     cc...............d...d...............cc
    cc...G..............Q..............G...cc
   cc...........dd.dd.d...d.dd.dd...........cc
  cc............d...d.dd.dd.d...d............cc
 cc...............Q...........Q...............cc
 c........dd.dd.d...d.dd.dd.d...d.dd.dd........c
cc........d...d.dd.dd.d...d.dd.dd.d...d........cc
c...........Q...........Q...........Q...........c
c...dd.dd.d...d.dd.dd.d...d.dd.dd.d...d.dd.dd...c
c...d...d.dd.dd.d...d.dd.dd.d...d.dd.dd.d...d...c
c.{...Q...........Q...........Q...........Q...{.c
c...d...d.dd.dd.d...d.dd.dd.d...d.dd.dd.d...d...c
c...dd.dd.d...d.dd.dd.d...d.dd.dd.d...d.dd.dd...c
c...........Q...........Q...........Q...........c
cc........d...d.dd.dd.d...d.dd.dd.d...d........cc
 c........dd.dd.d...d.dd.dd.d...d.dd.dd........c
 cc...............Q...........Q...............cc
  cc............d...d.dd.dd.d...d............cc
   cc...........dd.dd.d...d.dd.dd...........cc
    cc...G..............Q..............G...cc
     cc...............d...d...............cc
      ccc.............dd.dd.............ccc
        ccc...........................ccc
          cccc..........{..........cccc
             ccccc.............ccccc
                 ccccccccccccccc
ENDMAP

# 16 gods
NAME:   lightli_temple_of_artemis
TAGS:   no_pool_fixup
PLACE:  Temple
ORIENT: encompass
WEIGHT: 5
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KFEAT:  ~ = floor
MARKER: d = lua:fog_machine { cloud_type = "flame", pow_min = 100, \
                              pow_max = 100, delay = 1, start_clouds = 1, \
                              size = 1, spread_rate = 0, excl_rad = -1 }
COLOUR: c = white
COLOUR: . = lightgrey
COLOUR: ~ = green
FTILE:  .BQGd = floor_marble
FTILE:  ~t{ = floor_grass
TILE:   c = wall_marble
MAP
tttttttttttttttttttttttttttttttt
twwwwwwwwwwwwwwwwwwwwwwwwwwwwwwt
tw~~~~~~~~~~~~~~~~~~~~~~~~~~~~wt
tw~c~c~c~c~c~c~c~c~c~c~c~c~c~cwt
tw~~~~~~~~~~~~~~~~~~~~~~~~~~~~wt
tw~c~ccccccccccccccccccccccc~cwt
tw~~~c.................G...c~~wt
tw~c~c.....................c~cwt
tw~~~c.....Q..Q..Q..Q..Q...c~~wt
tw~c~cc.c...................~cwt
tw~~~cdQG..Q..Q..Q..Q..Q....~~{t
tw~c~cc.c...................~cwt
tw~~~c.....Q..Q..Q..Q..Q...c~~wt
tw~c~c.....................c~cwt
tw~~~c.................G...c~~wt
tw~c~ccccccccccccccccccccccc~cwt
tw~~~~~~~~~~~~~~~~~~~~~~~~~~~~wt
tw~c~c~c~c~c~c~c~c~c~c~c~c~c~cwt
tw~~~~~~~~~~~~~~~~~~~~~~~~~~~~wt
twwwwwwwwwwwwwwwwwwwwwwwwwwwwwwt
tttttttttttttttttttttttttttttttt
ENDMAP

# 18 gods
NAME:    regret_index_temple_chain_breaker
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: hi, qrs / qrs / rqs / rsq, xyz / xyz / yxz / yzx, SR, YZ
SUBST:   hqx = v, i : v.., j : vv., k : vv.
SUBST:   r : v.., y : v.., szRZ = ., S : v..., Y : v...
MAP
ccccccccccccccccccccccccccccccccccccccccccccc
c......vQv....................vQv...........c
c.v.v......Rv.vS.....h...v.v......Rv.vS.....c
cv...v.....v...v..v.....v...v.....v...v..v..c
c..Q..vv.vv..Q..vv.vj.....Q..vv.vv..Q..vv.vvc
cv...v.rqs.v...v..v.....v...v.rqs.v...v..v..c
c.v.v......Sv.vR.....i...v.v......Sv.vR.....c
c.....Zv.vY......v.v...i.....Zv.vY......v.v.c
c..v..v...v.zxy.v...v.....v..v...v.zxy.v...vc
cvv.vv..Q..vv.vv..Q.....kv.vv..Q..vv.vv..q..c
c..v..v...v.yxz.v...v.h...v..v...v.yxz.v...vc
c.....Yv.vZ......v.v.........Yv.vZ......v.v.c
c.....................{.....................c
c.v.v......Zv.vY.........v.v......Zv.vY.....c
cv...v.yxz.v...v..v...h.v...v.yxz.v...v..v..c
c..Q..vv.vv..Q..vv.vk.....Q..vv.vv..Q..vv.vvc
cv...v.zxy.v...v..v.....v...v.zxy.v...v..v..c
c.v.v......Yv.vZ.....i...v.v......Yv.vZ.....c
c.....Rv.vS......v.v...i.....Rv.vS......v.v.c
c..v..v...v.sqr.v...v.....v..v...v.sqr.v...vc
cvv.vv..Q..vv.vv..Q.....jv.vv..Q..vv.vv..Q..c
c..v..v...v.....v...v.....v..v...v.....v...vc
c.....Sv.vR......v.v...h.....Sv.vR......v.v.c
c...........vvv....................vQv......c
ccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

# 18 gods
NAME:    regret_index_temple_mists
TAGS:    no_rotate no_pool_fixup
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  5
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KFEAT:   F = w
SHUFFLE: HIJKLMN
SUBST:   HIJKL = Q, M : GTUtx, N : GTUtx, m : nwW, o : nwW
SUBST:   - : wxWW, ~ : wxWW, ' : wW, " : wW, ` : wW
MARKER:  F = lua:fog_machine { cloud_type = "thin mist", pow_min = 10, \
         pow_max = 14, delay = 60, start_clouds = 3, size_min = 6, \
         size_max = 8, walk_dist = 2, spread_rate = 6 }
MAP
    bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb
 bbbbnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnbb
 bnnnnWWWWWWWWWWWWWWWWWmmmmmWWWWWWWWWWWWWWWWWWWnnbb
 bnFWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWFnnb
bbnWWWWWW...WWWWWWWWWWWWWWWWWWWWWWWWWWW...WWWWWWWnb
bnnWWWWWW.Q.WWWWWWWWWWWW...WWWWWWWWWWWW.Q.WWWWWWWnb
bnWWWWWWW...WWWWW...WWWW.K.WWWW...WWWWW...WWWWWWWnb
bnWWWWWWWWWWWWWWW.J.WWWW...WWWW.L.WWWWWWWWWWWWWWWnb
bnWW...WWWWWWWWWW...WWWWWWWWWWW...WWWWWWWWWW...WWnb
bnWW.Q.WWW...WWWWWWWWW-WWWWW~WWWWWWWWW...WWW.Q.WWnb
bnWW...WWW.I.WWW'W'WW-WWWWWWW~WWW`W`WW.M.WWW...WWnb
bnWWWWWWWW...WW'W'WW-WWWWWWWWW~W`W`WWW...WWWWWWWWnb
bnoWWWWWWWWWWWWWWWWWWWWW...WWWWWWWWWWWWWWWWWWWWWonb
bnoWW...WWWW""WW...WWWW.....WWWW...WWWWWWWW...WWonb
bnoWW.H.WW""WWWW.N.WWWW..{..WWWW.N.WWWW""WW.H.WWonb
bnoWW...WWWWWWWW...WWWW.....WWWW...WW""WWWW...WWonb
bnoWWWWWWWWWWWWWWWWWWWWW...WWWWWWWWWWWWWWWWWWWWWonb
bnWWWWWWWW...WWW`W`W~WWWWWWWWW-WW'W'WW...WWWWWWWWnb
bnWW...WWW.M.WW`W`WWW~WWWWWWW-WW'W'WWW.I.WWW...WWnb
bnWW.Q.WWW...WWWWWWWWW~WWWWW-WWWWWWWWW...WWW.Q.WWnb
bnWW...WWWWWWWWWW...WWWWWWWWWWW...WWWWWWWWWW...WWnb
bnWWWWWWWWWWWWWWW.L.WWWW...WWWW.J.WWWWWWWWWWWWWWWnb
bnWWWWWWW...WWWWW...WWWW.K.WWWW...WWWWW...WWWWWWWnb
bnWWWWWWW.Q.WWWWWWWWWWWW...WWWWWWWWWWWW.Q.WWWWWWnnb
bnWWWWWWW...WWWWWWWWWWWWWWWWWWWWWWWWWWW...WWWWWWnbb
bnnFWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWFnb
bbnnWWWWWWWWWWWWWWWWWWWmmmmmWWWWWWWWWWWWWWWWWnnnnb
 bbnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnnbbbb
  bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb
ENDMAP

# 16 gods
NAME:    regret_index_temple_sphere_string
TAGS:    no_rotate
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  10
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: CD / CD / DC, EF / EF / FE, HI / HI / IH, JK / JK / KJ
SHUFFLE: defghi, jklmno
SUBST:   CEHJLMP = c, DFIKMOShino = .
SUBST:   L : cc., M : cc., N : cc., O : cc.
SUBST:   d : GGTUVc., j : GGTUVc.
SUBST:   e : cc., f : c., g : c.., k : cc., l : c., m : c..
MAP
                                        ccccccccc
                                      ccc.......ccc
                                    ccc...........ccc
                                  ccc......cOc......ccc
                                ccc.....IHc...cJK.....ccc
    cccccccccc              ccccc.......H...Q...J.......ccccc
  ccc........ccc          ccc...........c.j...k.c...........ccc
ccc............ccc      ccc............c...lmn...c............ccc
c......cMc.......ccc  ccc......ccc.....O.Q.o.o.Q.N.....cLc......c
c...DCc...cEF......cccc......cc...cc...c...nml...c..DCc...cEF...c
c...C...Q...E...............c.......c...c.k...j.c...C...Q...E...c
c...c.d...e.c...............c.......c...J...Q...H...c.d...e.c...c
c..c...fgh...c.............c.........c..KJc...cHI..c...fgh...c..c
c..M.Q.i.i.Q.L.....cNc.....c....{....c.....cNc.....L.Q.i.i.Q.M..c
c..c...hgf...c..IHc...cJK..c.........c.............c...hgf...c..c
c...c.e...d.c...H...Q...J...c.......c...............c.e...d.c...c
c...E...Q...C...c.j...k.c...c.......c...............E...Q...C...c
c...FEc...cCD..c...lmn...c...cc...cc......cccc......FEc...cCD...c
c......cLc.....N.Q.o.o.Q.O.....ccc......ccc  ccc.......cMc......c
ccc............c...nml...c............ccc      ccc............ccc
  ccc...........c.k...j.c...........ccc          ccc........ccc
    ccccc.......J...Q...H.......ccccc              cccccccccc
        ccc.....KJc...cHI.....ccc
          ccc......cOc......ccc
            ccc...........ccc
              ccc.......ccc
                ccccccccc
ENDMAP

##############################################################################
# <<5>> Variable altars, generic layouts

# 10, 14, 18 gods
NAME:    compressed_dpeg_circular_temple
TAGS:    no_rotate no_pool_fixup temple_variable
TAGS:    temple_altars_10 temple_altars_14 temple_altars_18
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  16
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KMONS:   1 = electric eel
KMONS:   2 = lava snake
KFEAT:   1 = deep_water
KFEAT:   2 = lava
SHUFFLE: CDEF
# Altar rings and stairs, different in each setting.
: local altar_count = get_altar_count(_G, 20)
: if altar_count == "10" then
SUBST:      C = Q, E = G, DH<IJ = .
NSUBST:     F = 1:{ / *:.
: elseif altar_count == "14" then
SUBST:      CDEF = Q, CHIJ = ., < = {
: elseif altar_count == "18" then
SUBST:      QDCEFH = ., IJ = Q, < = {
: end
SUBST:   W : mvbG w:160
# Chances for filled (1/6), absent (3/6),
# and flooded (empty or occupied 1/12 each) center rings.
: local arnd = crawl.random2(30)
: if arnd < 5 then
SUBST:   mwWTvbG[ = c
: elseif arnd < 23 then
SUBST:   mwW = ., [ = {
: else
SUBST:   TW[ = w
:   if arnd < 28 then
NSUBST:  w = 6:1w / *:w
COLOUR:  m = lightblue
COLOUR:  c = blue
:   else
SUBST:   w = l
NSUBST:  l = 6:2l / *:l
COLOUR:  m = lightred
COLOUR:  c = red
:   end
: end
NSUBST:  <[{ = 1:{ / *:.
KPROP:   wl1 = no_tele_into
MAP
               ccccccccccccc
           ccccc...........ccccc
        cccc.........I.........cccc
      ccc........I...D...I........ccc
    ccc......I.Q.H...H...H.Q.I......ccc
   cc........H...............H........cc
  cc......IC......mmmmmmm......EI......cc
 cc.......H....mmmmwwwwwmmmm....H.......cc
 c...........mmmwwwwwwwwwwwmmm...........c
cc.....QI...mmwwwwwTwWwTwwwwwmm...IQ.....cc
c.......H..mmwwwwwwwwwwwwwwwwwmm..H.......c
c..........mwwwwwwwwwwwwwwwwwwwm..........c
c.<..F.I...mwwwWwTwww[wwwTwWwwwm...I.F....c
c..........mwwwwwwwwwwwwwwwwwwwm..........c
c.......H..mmwwwwwwwwwwwwwwwwwmm..H.......c
cc......I...mmwwwwwTwWwTwwwwwmm...IQ.....cc
 c...........mmmwwwwwwwwwwwmmm...........c
 cc.......J....mmmmwwwwwmmmm....J.......cc
  cc.......E......mmmmmmm......C.......cc
   cc.......IH...............HI.......cc
    ccc........Q.H.......H.Q........ccc
      ccc.......I....D....I.......ccc
        cccc.......I...I.......cccc
           ccccc...........ccccc
               ccccccccccccc
ENDMAP

# 9, 18 gods
NAME:   dpeg_three_leaves_temple
TAGS:   no_rotate temple_variable temple_altars_9 temple_altars_18
PLACE:  Temple
ORIENT: encompass
WEIGHT: 8
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 18)
: if altar_count == "9" then
SUBST:  C = .
: elseif altar_count == "18" then
SUBST:  C = Q, T = .
: end
MAP
      bbbb               bbbb
    bbbb.bb             bb.bbbb
    bb....bb           bb....bb
   bb......bbb       bbb......bb
  bb.....Q...bb     bb...Q.....bb
 bb..Q........bbb bbb........Q..bb
 bb.....C...C...bbb...C...C.....bb
bb....Q....T.....b.....T....Q....bb
bb........C....C...C....C........bb
 bbbbbbbb........{........bbbbbbbb
        bbbbbbbb...bbbbbbbb
              bb.C.bb
             bb.....bb
            bb..CTC..bb
           bb.........bb
          bb.....C.....bb
         bb...Q.....Q...bb
        bb...............bb
        bbbb.....Q.....bbbb
           bbbb.....bbbb
             bbbbbbbbb
ENDMAP

# 10, 14 gods
NAME:    dpeg_chambers_temple
TAGS:    no_rotate temple_variable temple_altars_10 temple_altars_14
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  8
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 14)
: if altar_count == "14" then
SUBST:   CDEF = Q, '"-~ = .
: elseif altar_count == "10" then
SHUFFLE: C' / D" / E- / F~
SUBST:   CD = Q, '" = ., EF-~ = v
: end
MAP
         vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
      vvvv.........................................vvvvv
   vvvv....vvv'vvvvvvv"vvvvvvv.vvvvvvv-vvvvvvv~vvv....vvvv
   v....vvvv'''''vvv"""""vvv.....vvv-----vvv~~~~~vvvv....vv
 vvv.vvvvvv'''C'''v"""D"""v...Q...v---E---v~~~F~~~vvvvvv.vvv
vv.....vvvv'''''''v"""""""v.......v-------v~~~~~~~vvvv.....vv
v.......vvvv'''''vvv"""""vvv.....vvv-----vvv~~~~~vvvv.......v
v...Q....vvvvv'vvvvvvv"vvvvvvv.vvvvvvv-vvvvvvv~vvvv.....Q...v
vv.....v...vvv'vvvvvvv"vvvvvvv.vvvvvvv-vvvvvvv~vvv..v......vv
 vvv.vvvvv.........................................vvvvv.vvv
  v..vvvv...........................................vvvv..v
  v.vvvvv.......T.............{.............T.......vvvvv.v
  v..vvvv...........................................vvvv..v
 vvv.vvvvv.........................................vvvvv.vvv
vv......v..vvv~vvvvvvv-vvvvvvv.vvvvvvv"vvvvvvv'vvv..v......vv
v...Q.....vvvv~vvvvvvv-vvvvvvv.vvvvvvv"vvvvvvv'vvvv.....Q...v
v.......vvvv~~~~~vvv-----vvv.....vvv"""""vvv'''''vvvv.......v
vv.....vvvv~~~~~~~v-------v.......v"""""""v'''''''vvvv.....vv
 vvv.vvvvvv~~~F~~~v---E---v...Q...v"""D"""v'''C'''vvvvvv.vvv
   v....vvvv~~~~~vvv-----vvv.....vvv"""""vvv'''''vvvv....v
   vvvv....vvv~vvvvvvv-vvvvvvv.vvvvvvv"vvvvvvv.vvv....vvvv
      vvvv.........................................vvvv
         vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP

# 9, 13, 16, 19 gods.
NAME:   compressed_dpeg_triangle_lava_temple
TAGS:   temple_variable no_rotate
TAGS:   temple_altars_9 temple_altars_12 temple_altars_15 temple_altars_19
PLACE:  Temple
ORIENT: encompass
WEIGHT: 16
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
# Completely different altar points between each setting.
: local altar_count = get_altar_count(_G, 20)
: if altar_count == "9" then
SUBST:  c = {, C = Q, `G = l, defDEFA = ., a = T
: elseif altar_count == "13" then
SUBST:  d = {, D = Q, `G = l, cefCEFA = ., a = T
: elseif altar_count == "16" then
SUBST:  e = {, E = Q, `G = l, acdfCDF = ., A : TUV...
: elseif altar_count == "19" then
SUBST:  f = {, F = Q, `acdeCDE = .
: else
SUBST:  c = {, C = Q, `G = l, defDEFA = ., A = T
: end
SUBST:  aA = ., X : xlllllll
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xllllllllllllllllllllllllllllXlllXllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xllllllllllllllllllllllllllllll`llllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllll...lllllllllllllllllllllllllllllx
xllllllllllllllllllllllllllll.....llllllllllllllllllllllllllllx
xlllllllllllllllllllllllllll...F...lllllllllllllllllllllllllllx
xllllllllllllllllllllllllll....E....llllllllllllllllllllllllllx
xlllllllllllllllllllllllll..F..c..F..lllllllllllllllllllllllllx
xllllllllllllllllllllllll......d......llllllllllllllllllllllllx
xlllllllllllllllllllllll...............lllllllllllllllllllllllx
xllllllllllllllllllllll.................llllllllllllllllllllllx
xlllllllllllllllllllll...F.E...A...E.F...lllllllllllllllllllllx
xllllllllllllllllllll....CD.........DC....llllllllllllllllllllx
xlllllllllllllllllll...........F...........lllllllllllllllllllx
xllllllllllllllllll............E............llllllllllllllllllx
xlllllllllllllllll....F.E.............E.F....lllllllllllllllllx
xllllllllllllllll....C.D.....D.C.D.....D.C....llllllllllllllllx
xlllllllllllllll...........F.......F...........lllllllllllllllx
xllllllllllllll................e................llllllllllllllx
xlllllllllllll...FE............f............EF...lllllllllllllx
xllllllllllll...DC........ED...a...DE........CD...llllllllllllx
xlllllllllll.......A.....F.....A.....F.....A.......lllllllllllx
xllllllllll.........................................llllllllllx
xllllllXll...FE.......D........F........D.......EF...llXllllllx
xllllllll....a........E.C......D......C.E........a....llllllllx
xllllllll..........F........E.....E........F..........llllllllx
xllllllXll...........................................llXllllllx
xlllllllllllll```````````````````````````````````lllllllllllllx
xlllllllllllllllllll```````````````````````lllllllllllllllllllx
xlllllllllllllllllllll```````````````````lllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xlllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 10, 14 gods
NAME:   zaba_magenta_temple
TAGS:   no_rotate temple_variable temple_altars_10 temple_altars_14
PLACE:  Temple
ORIENT: encompass
WEIGHT: 8
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 14)
: if altar_count == "14" then
SUBST:  T = Q
: end
COLOUR: c = magenta
MAP
ccccccccccccccccccccccccccccccccccccccccccc
cccc.......ccccccc.......ccccccc.......cccc
cc...Q...Q...ccc...T...T...ccc...Q...Q...cc
c.............c.............c.............c
cc...........ccc...........ccc...........cc
cccc...Q.............{.............Q....ccc
cc...........ccc...........ccc...........cc
c.............c.............c.............c
cc...Q...Q...ccc...T...T...ccc...Q...Q...cc
cccc.......ccccccc.......ccccccc.......cccc
ccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

# 10, 14 gods
NAME:    eino_temple_lava
TAGS:    temple_variable temple_altars_10 temple_altars_14
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  8
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 14)
: if altar_count == "14" then
SUBST:   C = Q, dg = .
: elseif altar_count == "10" then
SUBST:   C = ., Gd = c, g = G
: end
COLOUR:  c = red
MAP
       cccc    ccc
      cclccccccclcc
   ccccl.lcclccl.lcccc
  cclll...c...c...lllcc
  clll..C.G...G.Q..lllc
  cll.....d.Q.d.....llc
 ccl..Q...d...d......lcc
ccl.......g...g.......lcc
cl..C...............Q..lc
ccl...................lcc
 cccGddg.........gddGccc
 cc...................cc
 cl..Q......{......Q..lc
 cc...................cc
 cccGddg.........gddGccc
ccl...................lcc
cl..Q...............C..lc
ccl.......g...g.......lcc
 ccl......d...d...Q..lcc
  cll.....d.Q.d.....llc
  clll..Q.G...G.C..lllc
  cclll...c...c...lllcc
   ccccl.lcclccl.lcccc
      cclccccccclcc
       ccc     ccc
ENDMAP

# 11, 14 gods
NAME:    jpeg_temple_clearing
TAGS:    no_rotate no_pool_fixup
TAGS:    temple_variable temple_altars_11 temple_altars_14
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  8
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
# Stairs, tree paths, altars, decorative pillars.
: local altar_count = get_altar_count(_G, 14)
: if altar_count == "11" then
SUBST:   < = {, w = ., " : t', ( = Q, C = ., U = T, z = x
SHUFFLE: xGTUu
SUBST:   u = t
FTILE:   ' = floor_dirt
: elseif altar_count == "14" then
SUBST:   ( = {, < = w
SHUFFLE: '"w / "'W
SUBST:   "W = t, CT = Q, z = ., Ux : GUUTTxt
FTILE:   'w = floor_dirt
FTILE:   " = floor_grass
: end
FTILE:   .{tQxGTU = floor_grass
COLOUR:  . = green / brown w:2
MAP
                ttttttttt
           ttttttttttttttttt
         ttttttttt.....ttttttttt
      tttttttttt.........tttttttttt
    ttttttttt""..z.....z..""ttttttttt
   tttttttt""tt.....(.....tt""tttttttt
 ttttttttt"tttt...........tttt"ttttttttt
ttttt.....tttttt....z....tttttt.....ttttt
ttt...Q.....tttttt.....tttttt.....Q...ttt
tt...........ttttttt'ttttttt...........tt
tt.C...x..Q..ttttttt'ttttttt..Q..x...C.tt
tt...........tttttt'''tttttt...........tt
ttt..Q......t'''tt''w''tt'''t......Q..ttt
tttt......tttttt'''w<w'''tttttt......tttt
ttttt"tttttttttttt''w''tttttttttttt"ttttt
tttttt"tttttttttttt'''tttttttttttt"tttttt
ttttttt"tt......t''ttt''t......tt"ttttttt
 ttttttt......T..ttttttt..T......ttttttt
 tttttt...........ttttt...........tttttt
  ttttt.Q...U.....ttttt.....U...Q.ttttt
  ttttt...........ttttt...........ttttt
   ttttt.....Q...ttttttt...Q.....ttttt
    tttttt.....tt""ttt""tt.....tttttt
      ttttttttttttt"""ttttttttttttt
        ttttttttttttttttttttttttt
               ttttttttttt
ENDMAP

# 14, 18 gods
NAME:    eino_narrow_crystal_temple
TAGS:    temple_variable temple_altars_14 temple_altars_18
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  8
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: C' / D" / E` / F~ / G- / H;
SUBST:   CDE = Q, '"` = .
: local altar_count = get_altar_count(_G, 18)
: if altar_count == "14" then
SUBST:   F~G-H; = b
: elseif altar_count == "18" then
SUBST:   FG = Q, ~- = ., H; = b
: end
MAP
       bbbbb  bbbbb  bbbbb
       b'''b  b"""b  b```b
       b'C'b  b"D"b  b`E`b
       b'''bbbb"""bbbb```b
       bbbb'bb"b"b"bb`bbbb
          bb'.bb"bb.`bb
          bb.bbb"bbb.bb
bbbbb  bbbb.b'bb"bb`b.bbbb  bbbbb
b;;;b  b...bb'bb"bb`bb...b  b~~~b
b;H;b  b.Q.bbb'b"b`bbb.Q.b  b~F~b
b;;;bbbb...bbbb...bbbb...bbbb~~~b
bbbb;bb.bbb.bbb.Q.bbb.bbb.bb~bbbb
   bb;.bbbbb.bb...bb.bbbbb.~bb
   bb.b;;bbbb.bb.bb.bbbb~~b.bb
bbbb.bbbb;bbbb.b.b.bbbb~bbbb.bbbb
b---bbbbbb...bb...bb...bbbbbb---b
b-G-------.Q....{....Q.-------G-b
b---bbbbbb...bb...bb...bbbbbb---b
bbbb.bbbb~bbbb.b.b.bbbb;bbbb.bbbb
   bb.b~~bbbb.bb.bb.bbbb;;b.bb
   bb~.bbbbb.bb...bb.bbbbb.;bb
bbbb~bb.bbb.bbb.Q.bbb.bbb.bb;bbbb
b~~~bbbb...bbbb...bbbb...bbbb;;;b
b~F~b  b.Q.bbb`b"b'bbb.Q.b  b;H;b
b~~~b  b...bb`bb"bb'bb...b  b;;;b
bbbbb  bbbb.b`bb"bb'b.bbbb  bbbbb
          bb.bbb"bbb.bb
          bb`.bb"bb.'bb
       bbbb`bb.b"b.bb'bbbb
       b```bbbb"""bbbb'''b
       b`E`b  b"D"b  b'C'b
       b```b  b"""b  b'''b
       bbbbb  bbbbb  bbbbb
ENDMAP

# 10, 15 gods
NAME:    eino_wide_crystal_templ
TAGS:    no_rotate temple_variable temple_altars_10 temple_altars_15
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  8
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SHUFFLE: a'- / A"c, += / += / =+
SUBST:   a = b, '- = ., c = b, + = ., = = b
CLEAR:   A"
: local altar_count = get_altar_count(_G, 15)
: if altar_count == "10" then
SUBST:  C = .
: elseif altar_count == "15" then
SUBST:  TC = Q
: end
MAP
            bbbbb
         aaab...baaa
         a''-.{.-''a
        bb-bb...bb-bb
        b...bb.bb...b
        b.Q.......Q.b
    bbbbb...b...b...bbbbb
    b...bb+bb.Q.bb+bb...b
bbbbb.Q.+...b...b...+.Q.bbbbb
b...b...b.T.bb=bb.T.b...b...b
b.Q....bb...........bb....Q.b
b...b...bbbbb.Q.bbbbb...b...b
bbbbb.T....bb...bb....T.bbbbb
    b...b...bb=bb...b...b
    bbbbb.Q.......Q.bbbbb
        b...b...b...b
        bbbbb.C.bbbbb
            b...b
            bbbbb
ENDMAP

# 12, 16 gods
NAME:   nicolae_temple_x_marks_the_god
TAGS:   no_pool_fixup
TAGS:   temple_variable temple_altars_12 temple_altars_16
PLACE:  Temple
ORIENT: encompass
WEIGHT: 8
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 16)
: if altar_count == "12" then
SUBST:  D = Q, E = ., g : Ghhh, h : tTV.....
: elseif altar_count == "16" then
SUBST:  E = Q, Dgh = .
: else
SUBST:  D = Q, E = ., g : Ghhh, h : tTV.....
: end
SUBST:  C : ccccwl, c : ccccvb
MAP
     ccccccccccccccccc
   ccc..hCCCCCCCCCh..ccc
  cc.....CCCCCCCCC.....cc
 cc......gCCCCCCCg......cc
 c..Q.....CCCCCCC.....Q..c
cc........hCCCCCh........cc
c.....E....CCCCC....E.....c
c......D...gCCCg...D......c
ch......E...CCC...E......hc
cCCg........hCh........gCCc
cCCCCh....Q..C..Q....hCCCCc
cCCCCCCg...........gCCCCCCc
cCCCCCCCCh.......hCCCCCCCCc
cCCCCCCCCCC..{..CCCCCCCCCCc
cCCCCCCCCh.......hCCCCCCCCc
cCCCCCCg...........gCCCCCCc
cCCCCh....Q..C..Q....hCCCCc
cCCg........hCh........gCCc
ch......E...CCC...E......hc
c......D...gCCCg...D......c
c.....E....CCCCC....E.....c
cc........hCCCCCh........cc
 c..Q.....CCCCCCC.....Q..c
 cc......gCCCCCCCg......cc
  cc.....CCCCCCCCC.....cc
   ccc..hCCCCCCCCCh..ccc
     ccccccccccccccccc
ENDMAP

# 12, 18 gods
NAME:    hc_temple_tempest_star
TAGS:    no_rotate no_pool_fixup no_cloud_gen
TAGS:    temple_variable temple_altars_12 temple_altars_18
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  8
KFEAT:   Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 16)
# Randomize altars versus triangles of points.
SHUFFLE: QC, DE, FG
# Sometimes fill in the outer star
# and shove in all altars to the inner star.
: local altar_count = get_altar_count(_G, 18)
: if crawl.coinflip() then
SUBST:   'bFGz = x, d = ., e = y
:   if altar_count == "12" then
SUBST:   CD = Q, E = .
:   elseif altar_count == "18" then
SUBST:   CDE = Q
:   else
SUBST:   CDE = Q
:   end
: else
SUBST:    'e = ., d = y, z : cWwl., bG = Q, DE = .
:   if altar_count == "12" then
SHUFFLE:  FG
SUBST:    CF = .
:   elseif altar_count == "18" then
SUBST:    CF = Q
:   else
SUBST:    CF = Q
:   end
: end
SUBST:   y : cWwl
MAP
                                 xxx
                                xxzxx
                               xxzzzxx
                              xxzz'zzxx
                             xxzz'''zzxx
                            xxzz'''''zzxx
                           xxzz'''F'''zzxx
                          xxzz'''''''''zzxx
                         xxzz'''''''''''zzxx
                        xxzz''''''y''''''zzxx
                       xxzz''''''yyy''''''zzxx
xxxxxxxxxxxxxxxxxxxxxxxxzz''''''yyyyy''''''zzxxxxxxxxxxxxxxxxxxxxxxxx
xzzzzzzzzzzzzzzzzzzzzzzzz''''''yyy.yyy''''''zzzzzzzzzzzzzzzzzzzzzzzzx
xxzz''''''''''''''''''''''''''yyy...yyy''''''''''''''''''''''''''zzxx
 xxzz'''G''''''''''''''b'''''yyy..C..yyy'''''b''''''''''''''G'''zzxx
  xxzz''''''''''''''''''''''yyy......Eeee''''''''''''''''''''''zzxx
   xxzz'''''''yyyyyyyyyyeeeyyy..D...yyyyyyyyyyyyyyyyyyy'''''''zzxx
    xxzz'''''''yyy.Q.dd.E.yyy.....yyyyyyyyy..Q..ddddyy'''''''zzxx
     xxzz'''''''yyy..dD..yyyy....yyyyy..........E..yy'''''''zzxx
      xxzz'''''''yyy......yyy....y................yy'''''''zzxx
       xxzz'''''''yyy......yyy........yyyyyy.D...ey'''''''zzxx
        xxzz'''''''yyyy......yyy.........yyyyyy.ee'''''''zzxx
         xxzz''b''''yyyyyy........{........yyyyyy''''b''zzxx
        xxzz'''''''ee.yyyyyy.........yyy......yyyy'''''''zzxx
       xxzz'''''''ye...D.yyyyyy........yyy......yyy'''''''zzxx
      xxzz'''''''yy................y....yyy......yyy'''''''zzxx
     xxzz'''''''yy..E..........yyyyy....yyyy..Dd..yyy'''''''zzxx
    xxzz'''''''yydddd..C..yyyyyyyyy.....yyy.E.dd.C.yyy'''''''zzxx
   xxzz'''''''yyyyyyyyyyyyyyyyyyy...D..yyyeeeyyyyyyyyyy'''''''zzxx
  xxzz''''''''''''''''''''''eeeE......yyy''''''''''''''''''''''zzxx
 xxzz'''F''''''''''''''b'''''yyy..Q..yyy'''''b''''''''''''''F'''zzxx
xxzz''''''''''''''''''''''''''yyy...yyy''''''''''''''''''''''''''zzxx
xzzzzzzzzzzzzzzzzzzzzzzzz''''''yyy.yyy''''''zzzzzzzzzzzzzzzzzzzzzzzzx
xxxxxxxxxxxxxxxxxxxxxxxxzz''''''yyyyy''''''zzxxxxxxxxxxxxxxxxxxxxxxxx
                       xxzz''''''yyy''''''zzxx
                        xxzz''''''y''''''zzxx
                         xxzz'''''''''''zzxx
                          xxzz'''''''''zzxx
                           xxzz'''G'''zzxx
                            xxzz'''''zzxx
                             xxzz'''zzxx
                              xxzz'zzxx
                               xxzzzxx
                                xxzxx
                                 xxx
ENDMAP

# 9 to 12 gods
NAME:   chequers_temple_sunken
TAGS:   temple_variable
TAGS:   temple_altars_9 temple_altars_10 temple_altars_11 temple_altars_12
PLACE:  Temple
ORIENT: encompass
WEIGHT: 4
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
# XXX: The only temple with this...
KFEAT:  O = altar_ecumenical
: local altar_count = get_altar_count(_G, 12)
: nsubst("O = " .. altar_count .. ":Q / 1:O.... / *:.")
SUBST:  - = .W, x = .:5 Wx, G = .:20 G, z = wW
COLOUR: -S = green
: set_border_fill_type("open_sea")
MAP
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwzxxxxxzwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwzxG-Gxzwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwzx-{-xzwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzzzxG-Gxzzzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxxx---xxxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxO-----Oxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxO-----Oxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxO-----Oxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxO-----Oxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxO-----Oxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxO-----Oxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzx-------xzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxG-----Gxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzxxxxxxxxxzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwzzzzzzzzzzzwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
ENDMAP

##############################################################################
# <<6>> Variable altars, unusual layouts
# Dedications and odd variations on other vaults and non-temple gods.

# 9, 11 gods,
# plus 6 to 12 altars to Lugonu.
NAME:   kb_corrupted_temple
TAGS:   no_pool_fixup temple_variable temple_altars_9 temple_altars_11
PLACE:  Temple
ORIENT: encompass
WEIGHT: 2
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KMONS:  P = demonic plant
KFEAT:  L = altar_lugonu
SUBST:  . = .:70 x:15 w l:5, ' = ., x = xxxcv
: local altar_count = get_altar_count(_G, 11)
: nsubst("O = " .. altar_count .. ":Q / 6:L / * = LP")
: local crnd = crawl.random2(4)
: if crnd == 0 then
LROCKCOL: magenta
: elseif crnd == 1 then
LROCKCOL: lightblue
: elseif crnd == 2 then
LROCKCOL: lightcyan
: else
LROCKCOL: lightred
: end
MAP
       xxxxx xxxxx xxxxx
      xx.O.xxx.'.xxx.O.xx
      x...'.x.'...x.'...x
      x'.V.O''.O.''O.V.'x
      x.'...x...'.x...'.x
      xx.O.xxx.'.xxx.O.xx
 xxxxx xxxxxxx'..xxxxxxxxxxxxx
xx.O.xxx.'.xxx.'.xxx.'.xxx.O.xx
x.'...x...'...'.'.'.'...x...'.x
x'.V.O''.O.'.'.1.'.'.O.''O.V.'x
x...'.x.'.......'.....'.x.'...x
xx.O.xxx.'.xxx.'.xxx.'.xxx.O.xx
 xxxxxxxx'xxxx.'.xxxx'xxxxxxxx
      xx.O.xxx'..xxx.O.xx
      x.'...xx.'.xx...'.x
      xO.V.Oxx..'xxO.V.Ox
      x...'.xx.'.xx.'...x
      xx.'.xxx'..xxx.'.xx
       xxxxx xP{Px xxxxx
             xxxxx
ENDMAP

# 11, 15 gods
NAME:   minmay_freezing_vapour_temple
TAGS:   temple_variable temple_altars_11 temple_altars_15
PLACE:  Temple
ORIENT: encompass
WEIGHT: 4
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
MARKER:  F = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 3, pow_max = 5, delay = 5, start_clouds = 3, \
             size = 2, spread_rate = 0, excl_rad = 0 }
COLOUR:  c = white
FTILE:   'F = floor_frozen
FTILE:   .Q{ = floor_pebble_blue
MAP
ccccccccccccccccc
c...............c
c.F.F.F.F.F.F.F.c
c...............c
c.F.Q.Q.Q.Q.Q.F.c
c...............c
c.F.Q.......Q.F.c
c...............c
c.F.Q...{...Q.F.c
c...............c
c.F.Q.......Q.F.c
c...............c
c.F.Q.Q.Q.Q.Q.F.c
c...............c
c.F.F.F.F.F.F.F.c
c...............c
ccccccccccccccccc
ENDMAP

# 17, 18 gods
NAME:   minmay_temple_bridge_d
TAGS:   no_cloud_gen temple_variable temple_altars_17 temple_altars_18
PLACE:  Temple
ORIENT: encompass
WEIGHT: 2
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
SUBST:  A : TUV
: local altar_count = get_altar_count(_G, 18)
: if altar_count == "17" then
SUBST:  C = .
: elseif altar_count == "18" then
SUBST:  C = Q
: end
MAP
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
lllllllll...lll...llllllllll...lllllllll
llllllll.....l.lll.llllllll..Q..llllllll
llllllll..A..l.lvl.llll..ll.....llllllll
llllllll.....l.lll.lll.ll.l.Q.Q.llllllll
llllllll.....ll.l.lll.lll.l.....llllllll
llllllll.....ll.l.ll.ll..ll.Q.Q.llllllll
lllllll.......l.l.ll.l.lll.......lllllll
lllllll.........l.l.ll.l...Q.Q.Q.lllllll
lllllll...{..A..l.l..l.l.C.......lllllll
lllllll.........l.ll.l.l...Q.Q.Q.lllllll
lllllll.......lll.l.ll.l.l.......lllllll
llllllll.....ll..ll.ll.l.ll.Q.Q.llllllll
llllllll.....l.lll.lll.l.ll.....llllllll
llllllll.....l.ll.lll.lll.l.Q.Q.llllllll
llllllll..A..ll..llll.lvl.l.....llllllll
llllllll.....llllllll.lll.l..Q..llllllll
lllllllll...llllllllll...lll...lllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
llllllllllllllllllllllllllllllllllllllll
ENDMAP

# 9, 16 gods
NAME:   kb_temple_of_zot
TAGS:   no_rotate temple_variable temple_altars_9 temple_altars_16
PLACE:  Temple
ORIENT: encompass
WEIGHT: 4
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 14)
: if altar_count == "9" then
SUBST:      C = Q, D = .
: elseif altar_count == "16" then
SUBST:      D = Q, C = .
: end
LFLOORCOL:  magenta
LROCKCOL:   lightmagenta
LFLOORTILE: floor_tomb
LROCKTILE:  wall_zot_magenta
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx......xxxxxx.......xxxxxx......xxxx
xxx....D...xxxx..DC.CD..xxxx...D....xxx
xx..........xxx.C.....C.xxx..........xx
xx.................Q.................xx
xxx.........xxx.C.....C.xxx.........xxx
xx...D......xxx..DC.CD..xxx......D...xx
xx..........xxxx.......xxxx..........xx
xxx........xxxxxxxxxxxxxxxxx........xxx
xxxx......xxxxxxxxxxxxxxxxxxx......xxxx
xxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxx
xxxx.......xxxxxxxxxxxxxxxxx.......xxxx
xxx..............xxxxx..............xxx
xx...........xxx...x...xxx...........xx
xx..D........xxxx.....xxxx........D..xx
xx...........xxxx..D..xxxx...........xx
xx...........xxx.......xxx...........xx
xxx.........xxxx.D...D.xxxx.........xxx
xxxx...D...xxxx.........xxxx...D...xxxx
xxxxx.....xxx.............xxx.....xxxxx
xxxxxxxxxxxxxxxxxxx{xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 9, 11 gods
NAME:   grunt_temple_mini_profane
TAGS:   no_rotate temple_variable temple_altars_9 temple_altars_11
PLACE:  Temple
ORIENT: encompass
WEIGHT: 2
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
MAP
    ccc      ccccccc      ccc
   cc.c      c.....c      c.cc
  cc..cccccccc..G..cccccccc..cc
 cc{..+......+.....+......+..Qcc
cc....+......+.....+......+....cc
c....Gcccccccc.....ccccccccG....c
ccc++ccxccc..+..Q..+..cccxcc++ccc
  c..cccc....+c...c+....cccc..c
  c..cc.....ccccccccc.....cc..c
  c..c...cc++...G...++cc...c..c
  c..c..ccc+.........+ccc..c..c
ccc++c++ccc...cc+cc...ccc++c++ccc
c......cccc..cc...cc..cccc......c
c.......ccc..c.Q.Q.c..ccc.......c
c.G...Q.cccG.+.....+.Gccc.Q...G.c
c.......ccc..c.Q.Q.c..ccc.......c
c......cccc..cc...cc..cccc......c
ccc++c++ccc...cc+cc...ccc++c++ccc
  c..c..ccc+.........+ccc..c..c
  c..c...cc++...G...++cc...c..c
  c..cc.....ccccccccc.....cc..c
  c..cccc....+c...c+....cccc..c
ccc++ccxccc..+..Q..+..cccxcc++ccc
c....Gcccccccc.....ccccccccG....c
cc....+......+.....+......+....cc
 ccQ..+......+.....+......+..Qcc
  cc..cccccccc..G..cccccccc..cc
   cc.c      c.....c      c.cc
    ccc      ccccccc      ccc
ENDMAP

# 11, 15, 17 gods,
# shameless.
NAME:   bh_familiar_temple
TAGS:   no_rotate no_hmirror no_vmirror
TAGS:   temple_variable temple_altars_11 temple_altars_15 temple_altars_17
PLACE:  Temple
ORIENT: encompass
WEIGHT: 2
KFEAT:  Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KFEAT:  S : abandoned_shop
# One door tile to every room, just like-
NSUBST:  d = 1:+ / *:c, e = 1:+ / *:c, f = 1:+ / *:c, g = 1:+ / *:c
NSUBST:  h = 1:+ / *:c, i = 1:+ / *:c, j = 1:+ / *:c, k = 1:+ / *:c
NSUBST:  l = 1:+ / *:c, m = 1:+ / *:c, n = 1:+ / *:c, o = 1:+ / *:c
NSUBST:  p = 1:+ / *:c, q = 1:+ / *:c, r = 1:+ / *:c, s = 1:+ / *:c
NSUBST:  t = 1:+ / *:c, u = 1:+ / *:c
SHUFFLE: '"
FTILE:   ' = floor_grass
FTILE:   " = floor_dirt
COLOUR:  ' = green
COLOUR:  " = brown
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x''''''''''''''''''''''{'''''''x
x'ccccccddccccccccccccc""ccccc'x
x'c.S..c..c...c..Q.c..e""f...c'x
x'c....cQ.c.Q.c....c..e""f.Q.c'x
x'cggggcccc...c+ccccccc""f...c'x
x'"""""cQ.c...c"c.Qc..c"cccccc'x
x'cccc"c..chhhc"c..c.Qc"i...Qc'x
x'cQ.j"ckkc""T""cllc..c"i....c'x
x'c..j"""""""""""""cmmc"i....c'x
x'ccccnnnc"ccccooc""""""iQ...c'x
x'cQ.c...c"p..c..cqqqqcccccccc'x
x'c..c.Q.c"p.QcQ.c....cQ.cQ..s'x
x'c..c...c"cccc..c..Q.c..c...s'x
x'cttccccc""T"cccccccccuuccccc'x
x''''''''''''''''''''''''''''''x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 9, 11 gods
# plus 4 or 5 more altars to Jiyva.
# Layout based on zaba_magenta_temple_12.
NAME:      pf_slimified_temple
TAGS:      no_rotate temple_variable temple_altars_9 temple_altars_11
PLACE:     Temple
ORIENT:    encompass
WEIGHT:    2
KFEAT:     Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
KFEAT:     J = altar_jiyva
KFEAT:     S = slimy_wall
SUBST:     - = -.
SUBST:     R = JQ
LFLOORCOL: green
COLOUR:    - = lightgray
FTILE:     .JRQ = floor_slime
MAP
SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS
SSSS...S...SSSSSSS.......SSSSSSS...S...SSSS
SS...R...R...SSS...R...R...SSS...R...R...SS
S......-......S.....---.....S......-......S
SS....-----.......------........-----....SS
SSS.R.---------------{---------------.J.SSS
SS....-----........------.......-----....SS
S......-......S.....---.....S......-......S
SS...R...R...SSS...R...R...SSS...R...R...SS
SSSS...S...SSSSSSS.......SSSSSSS...S...SSSS
SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS
ENDMAP

# 9, 11 gods,
# plus 5 to 7 more altars to Jiyva.
NAME:      pf_slime_end_temple
TAGS:      temple_variable temple_altars_9 temple_altars_11
PLACE:     Temple
ORIENT:    encompass
WEIGHT:    2
KFEAT:     Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 11)
: nsubst("J = " .. altar_count .. ":Q / *:J")
KFEAT:     J = altar_jiyva
KFEAT:     S = slimy_wall
SUBST:     " : " .:3,  " = .c, ' : ' .:3,  ' = .S
SUBST:     - = --.
LFLOORCOL: green
COLOUR:    - = lightgray
FTILE:     .J = floor_slime
: set_feature_name("stone_wall", "rune carved stone wall")
MAP
SSSSSSSSSSSSSSSSSSSSSSSSSSSS
SSSSSSSS''''''''''''SSSSSSSS
SSSSSSS'............'SSSSSSS
SSSSSS'..............'SSSSSS
SSSSS'...""""."""""...'SSSSS
SSSS'..."cccc.ccccc"...'SSSS
SSS'..."ccJcc..ccJcc"...'SSS
SS'..."cc...cc.c...cc"...'SS
S'..."ccJ..cc..cc..Jcc"...'S
S'.."cc....Jc.ccJ....cc"..'S
S'.."cc....cc..cc....cc"..'S
S'.."cccJ.c.cc.c.c.Jccc"..'S
S'.."ccccccc....ccccccc"..'S
S..-.c...c....J...c...c"..'S
S.-{-.c...c....c...c......'S
S..-.ccccccc....ccccccc"..'S
S'.."cccJ.c.cc.c.c.Jccc"..'S
S'.."cc....cc..cc....cc"..'S
S'.."cc....Jc.ccJ....cc"..'S
S'..."ccJ..cc..cc..Jcc"...'S
SS'..."cc...cc.c...cc"...'SS
SSS'..."ccJcc..ccJcc"...'SSS
SSSS'..."cccc.ccccc"...'SSSS
SSSSS'...""""."""""...'SSSSS
SSSSSS'..............'SSSSSS
SSSSSSS'............'SSSSSSS
SSSSSSSS''''''''''''SSSSSSSS
SSSSSSSSSSSSSSSSSSSSSSSSSSSS
ENDMAP

# 12, 15, or 18 gods.
NAME:    nicolae_temple_no_exploring_needed
TAGS:    no_pool_fixup
TAGS:    temple_variable temple_altars_12 temple_altars_15 temple_altars_18
PLACE:   Temple
ORIENT:  encompass
WEIGHT:  6
KFEAT:    Q = altar_the_shining_one / altar_zin / altar_elyvilon / altar_cheibriados
: local altar_count = get_altar_count(_G, 18)
: if altar_count == "12" then
SUBST:   QCEFHIKLNORS = _
SUBST:   AGM = p, DJP = q
SHUFFLE: pq / rr / rr
SUBST:   p : TG, q : TG, r = .
: elseif altar_count == "15" then
SUBST:   QCDEFHIJKLNOPRS = _
SUBST:   AGM : TG
: elseif altar_count == "18" then
SUBST:   AQCDEFGHIJKLMNOPRS = _
: else
SUBST:   AQCGHIMNO = _, DEFJKLPRS = Y
: end
SUBST:   _ = Q
SHUFFLE: abdefgh
SUBST:   abd = w, e : .w, f : .w, g : .w, h : .w
MAP
   ccccccccc
  ccD.E.F.Gcc
 cc.........cc
ccC..gh.hg..Hcc
c...bef.feb...c
cQ.gead.daeg.Ic
c..hfd...dfh..c
cA.....{.....Jc
c..hfd...dfh..c
cR.gead.daeg.Kc
c...bef.feb...c
ccS..gh.hg..Lcc
 cc.........cc
  ccP.O.N.Mcc
   ccccccccc
ENDMAP
